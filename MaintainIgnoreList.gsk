#*******************************************
# MacDescription = Maintain gc.com and GSAK Ignore Lists
# MacFileName =MaintainIgnoreList.gsk
# MacAuthor = Kai Team
# MacVersion=1.2
#*******************************************
$AddCodes=""
$CodesToCheck=""
$NeverRB=TRUE
$ThisTimeOnlyRB=FALSE
$EveryTimeRB=FALSE
MACSETTINGS Type=R FileCheck=N

#Show Options form if shift key pressed
IF $_ShiftKey
    WHILE TRUE
        $FormExit = form($OptionsForm,"")
        BEGINCASE
            CASE $FormExit = "OK"
                MACSETTINGS Type=S Vars=NeverRB,ThisTimeOnlyRB,EveryTimeRB
                BREAK
            CASE $FormExit = "SystemExit"
                RETURN Msg="Macro Canceled"
        ENDCASE
    ENDWHILE
ENDIF

#Get User's list of Bookmark lists, including GUID's, and extract GUID for Ignore List
$data = GcApi("GetBookmarkListsForUser")
IF $_GcApiError
    MSGOK msg=$data
ELSE
    $_sql="SELECT data FROM gcapi WHERE key LIKE '%/ListGUID' AND rowid < "
    $_sql=$_sql + "(SELECT rowid FROM gcapi WHERE key LIKE '%/ListName' AND data='Ignore List') ORDER BY rowid DESC LIMIT 1"
    $GUID=Sqlite("sql",$_sql)
ENDIF

#Get List of cache codes in the Ignore List Bookmark List, using GUID
$APIData=Replace("~~GUID~~",$GUID,$APIData)
$BookmarkData = GcApi("GetBookmarkListByGuid",$APIData)
IF $_GcApiError
    MSGOK msg= Error: $data
ELSE
    $GCIgnoreCodes = sqlite("sql","select data from GcApi where g_Contains('CacheCode',key)")
ENDIF

#Format list of codes on Ignore List to be used in SQLite IN() statement
$GCICInList=Replace($_CrLf,"','",$GCIgnoreCodes)
$GCICInList="'" + $GCICInList + "'"

#Get the list of codes in GSAK's ignore list that are not in geocaching.com ignore list, if any
$_sql="Select iCode from Ignore WHERE iCode NOT IN($GCICInList)"
$GSAKOnlyCodes=Sqlite("sql",$_sql)

#Get the list of codes and names in GSAK's list that are not in gc.com list (for form display purposes only)
$_sql="Select iCode,iName from Ignore WHERE iCode NOT IN($GCICInList)"
$GSAKOnlyCodesNames=Sqlite("sql",$_sql)

IF $GSAKOnlyCodes<>""
    #Format codes and names for display in the form.
    $CheckList=Replace(";","::",$GSAKOnlyCodesNames)
    $CheckList=Replace($_CrLf,";",$CheckList)

    #Show form with list of caches in GSAK list but not on gc.com list
    $Form1 = editform($Form1,"GSAKOnlyCodesList","values",$Checklist)
    WHILE TRUE AND $GSAKOnlyCodes<>""
        $FormExit = form($Form1,"")
        BEGINCASE
            CASE $FormExit = "SystemExit"
            RETURN Msg="Macro Canceled"

            CASE $FormExit = "SelectAll"
                $GSAKOnlyCodesList=$CheckList

            CASE $FormExit = "ClearAll"
                $GSAKOnlyCodesList=""

            CASE $FormExit = "OK"
            BREAK

            CASE $FormExit = "Cancel"
            RETURN Msg="Macro Canceled"
        ENDCASE
    ENDWHILE

    #Modify results to make code extraction easier
    $GSAKOnlyCodesList=";" + $GSAKOnlyCodesList

    #Extract codes from results and construct XML statement for API data statement
    $Count=RegExCount(";",$GSAKOnlyCodesList)
    $x=1
    WHILE $x<=$Count
        $ThisCode=RegExData(";.{3,8}::",$GSAKOnlyCodesList,$x)
        $ThisCode=Replace(";","",$ThisCode)
        $ThisCode=Replace("::","",$ThisCode)
        $AddCodes=$AddCodes + "<string xmlns=" + $_Quote + "http://schemas.microsoft.com/2003/10/Serialization/Arrays" + $_Quote + ">" + $ThisCode + "</string>" + $_CrLf
        $x=$x+1
    ENDWHILE


    #Update API data statement to contain GUID and List of Codes to be ignored
    IF $GSAKOnlyCodesList<>";"
        $AddtoIgnoreList=Replace("~~GUID~~",$GUID,$AddtoIgnoreList)
        $AddtoIgnoreList=Replace("~~Codes~~",$AddCodes,$AddtoIgnoreList)

        $Status=GcApi("AddGeocachesToBookmarkList",$AddtoIgnoreList)
        IF $_GcApiError
            MSGOK msg=$data
        ELSE
            MSGOK Msg=Selected caches were added to your geocaching.com ignore lsit
        ENDIF
    ELSE
        MSGOK Msg=No caches were selected to be added to your online ignore list.
    ENDIF
ELSE
    MSGOK Msg=All caches ignored in GSAK are also ignored at Geocaching.com
ENDIF

#Remove archived caches if selected
IF $ThisTimeOnlyRB OR $EveryTimeRB
    #Remove sinle quotes from list of codes ignored at gc.com (uses list generated above)
    $GCICodeslist=Replace("'","",$GCICInList)
    #Count number of codes on GC.com ignore list
    $iCount=RegExCount("\x2C",$GCICodeslist) + 1
    $x=1
    WHILE $x<=$iCount
        $ThisiCode=Extract($GCICodeslist,",",$x)
        $CodesToCheck=$CodesToCheck + "<string xmlns=" + $_Quote + "http://schemas.microsoft.com/2003/10/Serialization/Arrays" + $_Quote + ">" + $ThisiCode + "</string>" + $_CrLf
        $x=$x+1
    ENDWHILE

    $StatusCheck=Replace("~~iCodes~~",$CodesToCheck,$StatusCheck)
    $StatusCheck = GcApi("GetGeocacheStatus",$StatusCheck)

    #Select code from GcApi for archived caches
    IF $_GcApiError
        MSGOK msg=$StatusCheck
    ELSE
        $_sql="SELECT data FROM gcapi WHERE key LIKE '%CacheCode' AND rowid IN"
        $_sql=$_sql + "(SELECT rowid+2 FROM gcapi WHERE key LIKE '%Archived%' AND data='true') ORDER BY rowid DESC"
        $ArchivedCodes=Sqlite("sql",$_sql)
    ENDIF

    IF $ArchivedCodes=""
        RETURN Msg=There are no archived caches on your Geocaching.com Ignore List
    ELSE
        $Memo1=$ArchivedCodes
        WHILE TRUE
            $FormExit = form($ShowArchivedForm,"")
            BEGINCASE
                CASE $FormExit = "SystemExit"
                RETURN Msg="Macro Canceled"

                CASE $FormExit = "OK"
                BREAK

                CASE $FormExit = "CopyToClip"
                CLIP Data=$Memo1
                WEB URL="http://www.geocaching.com/bookmarks/default.aspx"
                RETURN
            ENDCASE
        ENDWHILE
    ENDIF

#    Semi-developed Code for possible future expansion, if gc.com adds "Remove from bookmark list" to the API
#    Correct API call and API data statement needed.
#    #Delete archived caches from gc.com bookmark list and GSAK ignore list
#    #Build data statement:
#    $ArchivedCodes=Replace($_CrLf,",",$ArchivedCodes)
#    $aCount=RegExCount("\x2C",$ArchivedCodes)
#    $x=1
#    WHILE $x<=$aCount + 1
#        $ThisaCode=Extract($ArchivedCodes,",",$x)
#        $CodestoRemove=$CodestoRemove + "<string xmlns=" + $_Quote + "http://schemas.microsoft.com/2003/10/Serialization/Arrays" + $_Quote + ">" + $ThisaCode + "</string>" + $_CrLf
#        $x=$x+1
#    ENDWHILE
#
#    #Update API data statement to contain GUID and List of Codes to be ignored
#    IF $aCount>0
#        $AddtoIgnoreList=Replace("~~GUID~~",$GUID,$AddtoIgnoreList)
#        $AddtoIgnoreList=Replace("~~Codes~~",$AddCodes,$AddtoIgnoreList)
#
#        $Status=GcApi("AddGeocachesToBookmarkList",$AddtoIgnoreList)
#        IF $_GcApiError
#            MSGOK msg=$data
#        ELSE
#            MSGOK Msg=Archived caches were removed from your geocaching.com ignore lsit
#        ENDIF
#
    IF $ThisTimeOnlyRB
        $EveryTimeRB=FALSE
        $ThisTimeOnlyRB=FALSE
        $NeverRB=TRUE
        MACSETTINGS Type=S Vars=NeverRB,ThisTimeOnlyRB,EveryTimeRB
    ENDIF
ENDIF

<Data> VarName=$form1
#********************************************************************
# Form generated by GSAK form designer on Mon 09-Apr-2012 19:13:21
#********************************************************************

Name = Form1
  Type = Form
  Caption = Caches ignored in GSAK but not at geocaching.com
  Height = 300
  Top = 356
  Width = 592

Name = OK
  Type = Button
  Height = 25
  Left = 132
  Top = 221
  Width = 75
  Taborder = 8
  Caption = OK

Name = Cancel
  Type = Button
  Height = 25
  Left = 303
  Top = 221
  Width = 75
  Taborder = 9
  Caption = Cancel

Name = Label1
  Type = Label
  Height = 16
  Left = 24
  Size = 10
  Top = 12
  Width = 361
  Caption = The folowing caches are ignored in GSAK but not at gc.com.

Name = GSAKOnlyCodesList
  Type = Checklistbox
  Height = 145
  Left = 24
  Top = 54
  Width = 439
  Taborder = 12

Name = Label2
  Type = Label
  Height = 17
  Left = 24
  Size = 10
  Top = 29
  Width = 440
  Caption = Check the caches that you want to add to your geocaching.com ignore list

Name = SelectAll
  Type = Button
  Height = 25
  Left = 486
  Top = 88
  Width = 75
  Taborder = 13
  Caption = Select All

Name = ClearAll
  Type = Button
  Height = 25
  Left = 486
  Top = 130
  Width = 75
  Taborder = 14
  Caption = Clear All

<enddata>

<Data> VarName=$form2
#********************************************************************
# Form generated by GSAK form designer on Sat 31-Dec-2011 16:06:52
#********************************************************************

Name = Form2
  Type = Form
  Caption = Caches ignored at geocaching.com but not ignored in this GSAK database
  Height = 300
  Top = 363
  Width = 500

Name = OK
  Type = Button
  Height = 25
  Left = 119
  Top = 205
  Width = 75
  Taborder = 8
  Caption = OK

Name = Cancel
  Type = Button
  Height = 25
  Left = 290
  Top = 205
  Width = 75
  Taborder = 9
  Caption = Cancel

Name = Label1
  Type = Label
  Height = 16
  Left = 24
  Size = 10
  Top = 12
  Width = 413
  Caption = The folowing caches are ingmored in gc.com but not in this GSAK DB

Name = GCcomOnlyCodesList
  Type = Checklistbox
  Height = 109
  Left = 24
  Top = 42
  Width = 439
  Taborder = 12

Name = Label2
  Type = Label
  Height = 17
  Left = 8
  Size = 10
  Top = 162
  Width = 468
  Caption = Check the caches that you want to add to the iignore list in this GSAK database

<enddata>

<Data> VarName=$OptionsForm
#********************************************************************
# Form generated by GSAK form designer on Thu 28-Jun-2012 19:14:44
#********************************************************************

Name = OptionsForm
  Type = Form
  Caption = Options
  Height = 198
  Width = 467

Name = Groupbox1
  Type = Groupbox
  Height = 103
  Left = 16
  Top = 14
  Width = 427
  Taborder = 2

Name = Label1
  Type = Label
  Container = Groupbox1
  Height = 17
  Left = 18
  Size = 10
  Top = 12
  Width = 400
  Caption = Check for archived caches on your geocaching.com ignore list:

Name = ThisTimeOnlyRB
  Type = Radiobutton
  Container = Groupbox1
  Exitonchange = No
  Fontsize = 10
  Height = 20
  Left = 18
  Top = 49
  Width = 205
  Taborder = 0
  Caption = Once (this time only)

Name = EveryTimeRB
  Type = Radiobutton
  Container = Groupbox1
  Exitonchange = No
  Fontsize = 10
  Height = 20
  Left = 18
  Top = 70
  Width = 199
  Taborder = 1
  Caption = Every time the macro runs

Name = NeverRB
  Type = Radiobutton
  Container = Groupbox1
  Exitonchange = No
  Fontsize = 10
  Height = 20
  Left = 18
  Top = 28
  Width = 98
  Taborder = 2
  Caption = Never

Name = OK
  Type = Button
  Alignment = Center
  Height = 25
  Left = 192
  Top = 130
  Width = 75
  Taborder = 11
  Caption = OK

<enddata>

<Data> VarName=$ShowArchivedForm
#********************************************************************
# Form generated by GSAK form designer on Sat 30-Jun-2012 12:59:07
#********************************************************************

Name = ShowArchivedForm
  Type = Form
  Caption=Archived Caches Report
  Height = 399
  Top = 371
  Width = 392

Name = OK
  Type = Button
  Height = 25
  Left = 70
  Top = 326
  Width = 75
  Taborder = 8
  Caption = Exit

Name = CopyToClip
  Type = Button
  Height = 25
  Left = 205
  Top = 326
  Width = 109
  Taborder = 9
  Caption = Copy to Clipboard

Name = Label1
  Type = Label
  Height = 16
  Left = 23
  Size = 10
  Top = 12
  Width = 326
  Caption = Archived Caches on your Geocaching.com Ignore List

Name = Memo1
  Type = Memo
  Height = 271
  Left = 23
  Readonly = Yes
  Scrollbars = Vertical
  Top = 36
  Width = 338
  Taborder = 12

<enddata>




<Data> Varname=$APIData
<GetBookmarkListByGuidRequest xmlns="http://www.geocaching.com/Geocaching.Live/data">
<AccessToken>{ACCESSTOKEN}</AccessToken>
<BookmarkListGuid>~~GUID~~</BookmarkListGuid>
</GetBookmarkListByGuidRequest>
<enddata>

<Data> Varname=$AddtoIgnoreList
<AddGeocachesToBookmarkListRequest xmlns="http://www.geocaching.com/Geocaching.Live/data">
  <AccessToken>{ACCESSTOKEN}</AccessToken>
  <BookmarkListGuid>~~GUID~~</BookmarkListGuid>
  <CacheCodes>
    ~~Codes~~
  </CacheCodes>
</AddGeocachesToBookmarkListRequest>
<enddata>

<Data> Varname=$StatusCheck
<GetGeocacheStatusRequest xmlns="http://www.geocaching.com/Geocaching.Live/data">
  <AccessToken>{ACCESSTOKEN}</AccessToken>
  <CacheCodes>
    ~~iCodes~~
  </CacheCodes>
</GetGeocacheStatusRequest>
<enddata>

