#*******************************************
# MacDescription = FSG Plugin - Display the 1st cache you found in every county
# MacFileName = FSGPlugin_FirstCacheByCounty.gsk
# MacAuthor = The CEO
# MacVersion=1.05
# MacUrl=http://gsak.net/board/index.php?showtopic=17428&st=0&#entry122430
#*******************************************

IF $_MacroLevel = 1
  RETURN msg=This macro is a FindStatGen plug-in and cannot be run directly. $_CrLf To use the plugin, add the following line to one of the notes $_CrLf sections in the FindStatsGen macro: $_CrLf $_CrLF <plugin>FSGPlugin_FirstCacheByCounty</plugin>
ENDIF

	Declare Var=$p1 Type=String
	Declare Var=$p2 Type=String
	Declare Var=$p3 Type=String
	Declare Var=$p4 Type=String
	Declare Var=$tmpS2 Type=String
	Declare Var=$StateLimit Type=String
	Declare Var=$CountryLimit Type=String
	Declare Var=$ShowHeading Type=Boolean
	Declare Var=$UniqueStateCountyCombinations Type=Boolean
	
# check for macroparameters
$p1 = extract($_MacroParms,"&",1)
$p2 = extract($_MacroParms,"&",2)
$p3 = extract($_MacroParms,"&",3)
$p4 = extract($_MacroParms,"&",4)

# $msg = "p1=$p1" + $_NewLine + "p2=$p2" + $_NewLine + "p3=$p3"
# Msgok msg=$msg

If upper($p1) = upper("UniqueStateCountyCombinations=false") OR $p2 = upper("UniqueStateCountyCombinations=false") OR upper($p3) = upper("UniqueStateCountyCombinations=false") OR upper($p4) = upper("UniqueStateCountyCombinations=false")
	$UniqueStateCountyCombinations=FALSE
ELSE
	$UniqueStateCountyCombinations=TRUE
endif


If Left($p2,6) = "state=" 
	$tmpS=$p1
	$p1=$p2
	$p2=$tmpS
endif

If Left($p3,6) = "state=" 
	$tmpS=$p1
	$p1=$p3
	$p3=$tmpS
endif

If Left($p4,6) = "state=" 
	$tmpS=$p1
	$p1=$p4
	$p4=$tmpS
endif

If Left($p1,8) = "country=" 
	$tmpS=$p2
	$p2=$p1
	$p1=$tmpS
endif

If Left($p3,8) = "country=" 
	$tmpS=$p2
	$p2=$p3
	$p3=$tmpS
endif

If Left($p4,8) = "country=" 
	$tmpS=$p2
	$p2=$p4
	$p4=$tmpS
endif

$StateLimit=""
If Left($p1,6) = "state=" 
	$p1=Replace("state=","",$p1)
	$tmpN=1
	$tmpS2="x"
	WHILE $tmpS2<>""
		$tmpS2 = Extract($p1, ";",$tmpN)
		If $tmpS2<>""
			If $tmpN>1
				$StateLimit=$StateLimit+"OR "
			EndIf
			$tmpS = Extract($tmpS2, "@",2)
			If $tmpS<>""
				$StateLimit=$StateLimit+"(g_upper(State)=g_upper('"+Extract($tmpS2, "@",1)+"') AND g_upper(Country)=g_upper('"+Extract($tmpS2, "@",2)+"')) "
			ELSE
				$StateLimit=$StateLimit+"g_upper(State)=g_upper('$tmpS2') "
			EndIf
			$tmpN=$tmpN+1		
		EndIf
	ENDWHILE
$StateLimit="($StateLimit) "
#Msgok msg=$StateLimit
endif

$CountryLimit=""
If Left($p2,8) = "country=" 
	$p2=Replace("country=","",$p2)
	$tmpN=1
	$tmpS2="x"
	WHILE $tmpS2<>""
		$tmpS2 = Extract($p2, ";",$tmpN)
		If $tmpS2<>""
			If $tmpN>1
				$CountryLimit=$CountryLimit+"OR "
			EndIf
			$CountryLimit=$CountryLimit+"g_upper(Country)=g_upper('$tmpS2') "
			$tmpN=$tmpN+1		
		EndIf
	ENDWHILE
	$CountryLimit="AND ($CountryLimit) "
#Msgok msg=$CountryLimit
endif

If $CountryLimit<>"" and $StateLimit<>""
	$StateLimit=" OR $StateLimit"
EndIF

If $CountryLimit="" and $StateLimit<>""
	$StateLimit=" AND $StateLimit"
EndIF

If upper($p1) = upper("sectionheading=false") OR upper($p2) = upper("sectionheading=false") OR upper($p3) = upper("sectionheading=false") OR upper($p4) = upper("sectionheading=false")
	$ShowHeading=FALSE
ELSE
	$ShowHeading=TRUE
endif
	
	# Exclude locationless?
	IF $Excludelocationless
		$Locationless = "Cachetype <>'L' AND Exclude <> '1'"
	ELSE
		$Locationless = "1"
	ENDIF
	
	# find unique county+state combinations
	if $UniqueStateCountyCombinations=TRUE
		$_sql = "SELECT rowid, ldate, county, code, cachetype, name, llogid, url, country, state FROM allfinds WHERE rowid IN (SELECT min(rowid) FROM allfinds WHERE $Locationless AND county<>'' $CountryLimit $StateLimit GROUP BY county,state)"
		$work = Sqlite("sql",$_sql)
		$LogTotal = $_SqlRows
		# find rowid and "county [state] string" of all non unique counties
		$_sql= "SELECT rowid,  County||' ['||state||']' FROM allfinds WHERE rowid IN (SELECT min(rowid) FROM allfinds WHERE county<>''GROUP BY county,state) AND county in (SELECT county FROM allfinds WHERE rowid IN (SELECT min(rowid) FROM allfinds WHERE county<>'' GROUP BY county, state) and rowid NOT IN (SELECT min(rowid) FROM allfinds WHERE county<>'' GROUP BY county))"
		$tmpS2 = Sqlite("sql",$_sql)
	ELSE
		$_sql = "SELECT rowid, ldate, county, code, cachetype, name, llogid, url, country, state FROM allfinds WHERE rowid IN (SELECT min(rowid) FROM allfinds WHERE $Locationless AND county<>'' $CountryLimit $StateLimit GROUP BY county)"
		$work = Sqlite("sql",$_sql)
		$LogTotal = $_SqlRows
		# find rowid and "county [state] string" of all non unique counties (must return empty list because UniqueStateCountyCombinations=FALSE)
		$_sql= "SELECT rowid,  County||' ['||state||']' FROM allfinds WHERE 0"
		$tmpS2 = Sqlite("sql",$_sql)
	endif
	
#Msgok msg=$_sql	


	#Initialize Variables

	$out=""


	$counter=1
	
	# Initialise Section header
	$colspan = 1
	$text="First Cache By County"

	If $ShowHeading
		#define maximum table width
		if $maxtablewidth=0
			$maxtablewidth = 750
		endif
		GOSUB Name=SectionHead
	endif
	
	$out = $out + "<table width='$maxtablewidth' style='text-align: left;'>" + $_CrLf
	# Column Heads
	$text1 = "<b>$Lang31</b>" # Milestone
	$text2 = "<b>$Lang32</b>" # Date
	# $text7 = Flag image
	$text3 = "<b>County</b>" # County
	$text4 = "<b>$Lang34</b>" # Code
	# $text6 = Type icon
	$text5 = "<b>$Lang35</b>" # Name

	$out = $out + "<tr>" + "$_CrLf" + "<td style='$stTab2cHead'>" + $_CrLf
	$out = $out + "$text1</td>" + $_CrLf
	$out = $out + "<td style='$stTab2cHead'>$text2</td>" + $_CrLf
	IF $FlagIcons
		$out = $out + "<td style='$stTab2cHead'>&nbsp;</td>" + $_CrLf
	ENDIF
	$out = $out + "<td style='$stTab2cHead'>$text3</td>" + $_CrLf
	$out = $out + "<td style='$stTab2cHead'>$text4</td>" + $_CrLf
	IF $miletypes
		$out = $out + "<td style='$stTab2cHead'>&nbsp;</td>" + $_CrLf
	ENDIF
	$out = $out + "<td style='$stTab2cHead'>$text5</td>" + $_CrLf + "</tr>" + $_CrLf

	# Initialize variables

	$count = 1
#loop through all unique county+state combinations
WHILE $count <= $LogTotal
	$GetMilestone = Extract($work,$_CrLf,$count)
	#Extract the date
	$LogDateDate = SqlToDate(Extract($GetMilestone,";",2))

	#Strip off the suffix code if necessary
	$StripCode = Extract($GetMilestone,";",4)
	$StripCode = Extract($StripCode,"_",1)

	# Format the date to users standard format for display
	$FindDate = DateFormat($LogDateDate)

	# Log ID for URL link to log
	$LogidSubstitute = Val(Extract($GetMilestone,";",7))
	IF $LogidSubstitute <>0
		BEGINCASE
		CASE Left($StripCode,2)="GC"
			$text2 = "<a href='http://www.geocaching.com/seek/log.aspx?LID=$LogidSubstitute'>$FindDate</a>"
		CASE Left($StripCode,2)="GA"
			$text2 = "<a href='http://geocaching.com.au/log/$LogidSubstitute'>$FindDate</a>"
		OTHERWISE
			$text2 = "$FindDate"
		ENDCASE
	ELSE
		$text2 = "$FindDate"
	ENDIF

	# Output data
	$text1 = "<b>" + Extract($GetMilestone,";",1) + "</b>"
	# $text2 = "$FindDate"
	$text3 = Extract($GetMilestone,";",3)
	$text9 = Extract($GetMilestone,";",9)
	$text10 = Extract($GetMilestone,";",10)	

	# look in the second table if the row id exist there. If so, display county [state]  rather than only the county
	$tmpN=Val(Extract(Extract($tmpS2,$_CrLf,$counter),";",1))
	if $tmpN = val(Extract($GetMilestone,";",1))
		$text3=Extract(Extract($tmpS2,$_CrLf,$counter),";",2)
		$counter=$counter+1
	endif

	$text4 = Extract($GetMilestone,";",8)
	$text4 = "<a href='$text4'>$StripCode</a>"
	$text5 = Extract($GetMilestone,";",6)
	$text5 = "$text5"

	IF $miletypes
		$tmpB = FALSE
		GOSUB Name=StripCodeType
		$text6 = $TypeImage
	ELSE
		$text6 = ""
	ENDIF

	IF $FlagIcons
		# Country flag
		$FullName = $text9
		GOSUB Name=FlagImage
	ENDIF

	$out = $out + "<tr>" + "$_CrLf" + "<td style='$st2c1'>" + $_CrLf
	$out = $out + "$text1</td>" + $_CrLf
	$out = $out + "<td style='$st2c2'>$text2</td>" + $_CrLf
	IF $FlagIcons
		$out = $out + "<td style='$st2c2'>$text7</td>" + $_CrLf
	ENDIF
	$out = $out + "<td style='$st2c2'>$text3</td>" + $_CrLf
	
	$out = $out + "<td style='$st2c2'>$text4</td>" + $_CrLf
	IF $miletypes
		$out = $out + "<td style='$st2c2'>$text6</td>" + $_CrLf
	ENDIF
	$out = $out + "<td style='$st2c1'>$text5</td>" + $_CrLf + "</tr>" + $_CrLf
	$count = $count + 1
ENDWHILE


	$out = $out + "</table>" + $_CrLf

$p_FSGData = $out

BEGINSUB Name=FlagImage
	# Enter with $FullName containing the country name
	# Exits with $text7 containing the html to the flag Image
	$text7 = ""
	$tmpS1 = $FullName
	$FullName = "=" + $FullName + ","
	IF At($FullName,$world)>0
		$text7 = SubStr($world,At($FullName,$world)-2,2)
		IF $tmpS1 = "Hong Kong"
			$text7 = "HK"
		ENDIF
		$text7 = "<img align ='top' vspace='1'  alt='$tmpS1' title='$tmpS1' src='" + $ImageURL + "flags/" + $text7 + ".gif'/>"
	ENDIF
ENDSUB # FlagImage

BEGINSUB Name=StripCodeType
	#Enters with $Stripcode containing code
	# and $tmpB where TRUE = Placed table and FALSE = finds table
	#Exits with $TypeImage containing image of cache type
	$Types = "ABCEGLMRTUVWZXIY123"
	$Typenums = "9,5,13,6,27,12,3,137,2,8,4,11,453,2134,1858,waymark,3653,3773,3774"
	# If $tmpB is true then use the placed table instead
	IF $tmpB
		$_sql = "Select CacheType from PlacedStatsData WHERE Code like '$StripCode' LIMIT 1"
	ELSE
		$_sql = "Select CacheType from Allfinds WHERE Code like '$StripCode' LIMIT 1"
	ENDIF
	$tmpS = Sqlite("sql",$_sql)
	$tmpN = At($tmpS,$Types)
	$thistype	= Extract($Typenums, "," , $tmpN)
	BEGINCASE
		CASE $thistype = "Y"
			$TypeImage = "<img align='top' src='http://gsak.net/stats/wm16.gif' />"
		CASE $thistype = "L"
			$TypeImage = "<img align='top' src='http://gsak.net/stats/1216.gif' />"
		OTHERWISE
			$TypeImage = "<img align='top' src='http://www.geocaching.com/images/wpttypes/sm/" + $thistype + ".gif' />"
	ENDCASE
ENDSUB #StripCodeType

BEGINSUB Name=SectionHead
	# This subroutine is called by all the stats generating routines.
	# It writes the section header and also handles the half width sections
	# intelligently .
	# Add a couple of blank lines to the output variable
	$out = $out + "<br /><br />" + $CR
	$out = $out + "<!-- SectionHead CS=$colspan LCS=$lastcolspan C=$column $text -->" + $CR
	IF $colspan = 1
		# This is a full width section
		# If the last section was a half width close the previous table off
		IF $lastcolspan = 2
			GOSUB Name=Close2col
		ENDIF
		$lastcolspan = 1
		$column = 1
		$width = $maxtablewidth
	ELSE
		# This is a half width section
		IF $column = 1
			# First Column
			# If we're following another half width block, close the table off
			IF $lastcolspan = 2
				GOSUB Name=Close2col
			ENDIF
			GOSUB Name=Open2Col
			$column = 2
		ELSE
			# Second Column
			GOSUB Name=Mid2Col
			$column = 1
		ENDIF
		$lastcolspan = 2
		$width = $maxtablewidth/2
	ENDIF
	$tmpS = ""
	IF $Placed
		$tmpS = $stYearDataTit
	ENDIF
	# And add the header to the output variable
	$out = $out + "<div style='width:$width" + "px; $stSectHead; $tmpS'>" + $CR
	$out = $out + "<a name='$Srun'></a>"
	$out = $out + "    $text" + $CR + "</div><br />" + $CR
ENDSUB #SectionHead
