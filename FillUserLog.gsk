################################################################################
# MacVersion = 0.2
# MacDescription = Fill the "User Logs" section with your logs
# MacAuthor = Bushman4
# MacFileName = FillUserLog.gsk
# MacUrl = http://gsak.net/board/index.php?showtopic=5407&view=getlastpost
################################################################################

Option Explicit=Yes

Declare Var=$Completed Type=Numeric
Declare Var=$strLogs Type=String
Declare Var=$strUserLog Type=String
Declare Var=$strUserNote Type=String
DECLARE Var=$LogId Type=Numeric

TRANSACTION Action=Begin
$Completed = 0
MFilter Expression=$d_Found
Goto Position=Top
While Not($_eol)
        $Completed = $Completed + 1
        If Frac($Completed/10) = 0
                SHOWSTATUS msg="Processing cache $Completed"
        EndIF
        $strUserNote = ""
        $strUserLog = ""
        If $d_UserNote <> ""
                $strUserNote = Extract($d_UserNote, "$~", 1)
                $strUserLog = Extract($d_UserNote, "$~", 2)
        EndIf
        Table Active=Logs Scope=Parent
        $strLogs = ""
        $LogId = 0
        Goto Position=Top
        While Not($_EOL)
                If IsOwner()
                        $strLogs = $strLogs + "$d_lDate" + $_NewLine + "$d_lType" + $_NewLine + $_NewLine + "$d_lText" + $_NewLine + "------------------------------------" + $_NewLine + $_NewLine
                        $LogId = $d_lLogId
                EndIf
                Goto Position=Next
        EndWhile
        
        Table Active=Caches
        If $strLogs <> "" and $strLogs <> $strUserLog
                $d_UserNote= $strUserNote + "$~" + $strLogs
        EndIf
        $d_UserSort=$LogId
        Goto Position=Next
        
ENDWHILE
TRANSACTION Action=End