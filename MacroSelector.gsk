#*******************************************
# MacDescription = Run selected macros from a dialog
# MacFileName = MacroSelector.gsk
# MacAuthor = Kai Team
# MacVersion=1.4
# MacUrl=http://gsak.net/board/index.php?showtopic=9662&view=findpost&p=62345
#*******************************************

$MacroPath=$_Install + "\Macros"
$comspec = GetEnvV("comspec")
$Directory=$MacroPath + "\*.gsk"
$DataFile=$MacroPath + "\MacroSelector.dat"
$FileList=$_Install + "\Temp\FileList.txt"

IF NOT(FileExists($DataFile))
    GOSUB Name=Configure
ELSE
    $Checklistbox1 = GetFile($DataFile)
    IF Left($Checklistbox1,7) = "*Error*"
        PAUSE Msg="$Checklistbox1"
        CANCEL 
    ENDIF
    $RunForm = editform($RunForm,"Combobox1","values",$Checklistbox1)
ENDIF

WHILE TRUE
    $FormExit = form($RunForm,"")
    BeginCase
        CASE $FormExit = "SystemExit"
            CANCEL Msg="Macro Cancelled"

        CASE $FormExit = "Run"
            $File=$MacroPath + "\" + $Combobox1
            MACRO File=$File
            BREAK
            
        CASE $FormExit="Configure"
            GOSUB name=Configure

        CASE $FormExit = "Cancel"
            CANCEL Msg="Macro Cancelled"
    ENDCASE 
ENDWHILE    


BEGINSUB Name=Configure
    RUNPGM pgm=$comspec parms=/C DEL "$FileList" Wait=Yes
    RUNPGM pgm=$comspec parms=/C DIR "$Directory" /a:-d /b  > "$FileList"  Wait=Yes
    $MacroFiles = GetFile($FileList)
    IF Left($MacroFiles,7) = "*Error*"
        Pause Msg=$MacroFiles
        CANCEL 
    ENDIF
    
    $MacroFiles=Replace($_NewLine,";",$MacroFiles,TRUE)
    $SelectForm = editform($SelectForm,"CheckListBox1","values",$MacroFiles)
    
    WHILE TRUE
        $FormExit = form($SelectForm,"")
        BeginCase
            CASE $FormExit = "SystemExit"
            CANCEL Msg="Macro Cancelled"
    
            CASE $FormExit = "OK"
                $Data = PutFile($DataFile,$Checklistbox1)
                IF Left($Data,7) = "*Error*"
                    PAUSE  Msg="$Data"
                    CANCEL 
                ENDIF
                $RunForm = editform($RunForm,"Combobox1","values",$Checklistbox1)
            BREAK 
    
            CASE $FormExit = "Cancel"
            CANCEL Msg="Macro Cancelled"
        ENDCASE 
    ENDWHILE
ENDSUB

<Data> VarName=$Selectform
#********************************************************************
# Form generated by GSAK form designer on Tue 07-Oct-2008 19:40:01
#********************************************************************

Name = Form1
  Type = Form
  Caption = Check each macro to include in the run dialog
  Color = 16771818
  Height = 637
  Width = 810

Name = Label1
  Type = Label
  Height = 20
  Left = 30
  Size = 12
  Top = 18
  Width = 563
  Caption = Place a check mark next to each macro that you want to include in the run dialog

Name = Checklistbox1
  Type = Checklistbox
  Columns = 4
  Height = 493
  Left = 31
  Top = 54
  Values = $MacroFiles
  Width = 739
  Taborder = 8

Name = OK
  Type = Button
  Height = 25
  Left = 261
  Top = 564
  Width = 75
  Taborder = 9
  Caption = OK

Name = Cancel
  Type = Button
  Height = 25
  Left = 465
  Top = 564
  Width = 75
  Taborder = 10
  Caption = Cancel

<enddata>

<Data> VarName=$Runform
#********************************************************************
# Form generated by GSAK form designer on Wed 08-Oct-2008 18:22:22
#********************************************************************

Name = Form2
  Type = Form
  Caption = Run a macro
  Color = 15984856
  Height = 156
  Width = 381

Name = Label1
  Type = Label
  Height = 16
  Left = 24
  Size = 10
  Top = 12
  Width = 128
  Caption = Select a macro to run:

Name = Combobox1
  Type = Combobox
  Height = 21
  Left = 21
  Top = 36
  Values = $Checkboxlist1
  Width = 331
  Taborder = 8

Name = Run
  Type = Button
  Height = 25
  Left = 22
  Top = 78
  Width = 75
  Taborder = 9
  Caption = Run

Name = Cancel
  Type = Button
  Height = 25
  Left = 149
  Top = 78
  Width = 75
  Taborder = 10
  Caption = Cancel

Name = Configure
  Type = Button
  Height = 25
  Left = 276
  Top = 78
  Width = 75
  Taborder = 11
  Caption = Configure List

<enddata>

