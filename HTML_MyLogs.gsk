#***************************************
# HTMLName=My Logs
# SplitScreen=Yes
# Export=No
# Offline=No
#***************************************

# MacVersion = 1.6
# MacDescription = My Logs Split Screen macro
# MacAuthor = Lignumaqua
# MacFileName = HTML_MyLogs.gsk

#***************************************
#   My Logs Split Screen
#   v1.1 modified  6/10/07
#   v1.2 9/26/09
#   v1.3 1/3/11 Smileys + SQLite
#   v1.4 2/14/12 Fix unclosed <p> and reverse log display order
#   v1.5 8/7/12 Replace HTML() with CustomFormat()
#   v1.6 12/24/12 Use BBCode()
#   Mike Wood (Lignumaqua)
#***************************************

Option Explicit=Yes

Declare Var=$_HTML Type=String
Declare Var=$date Type=String
Declare Var=$html Type=String
Declare Var=$Image Type=String
Declare Var=$ImageList Type=String
Declare Var=$ImagePreamble Type=String
Declare Var=$ImageSuffix Type=String
Declare Var=$List Type=String
Declare Var=$LogText Type=String
Declare Var=$status Type=String
Declare Var=$TypeList Type=String

VERCHECK version=7.7.0.0 (This macro needs the latest V7.7 OR later build - see http://gsak.net/board/index.php?showtopic=3552)
NOERROR Functions=BBCode

$html = CustomFormat("OpenDoc")
$html = $html + CustomFormat("OpenTables")
$html = $html + CustomFormat("OpenLeftCol")

$ImagePreamble = "<img src='file://" + $_ExePath + "/images/"

$ImageSuffix = "' border='0' width='15'  height='15'"

$List = List("TypeList","Create",";")
$TypeList = "Archive;Attended;Didn't find it;Enable Listing;Found it;Needs Archived;Needs Maintenance;Note;Owner Maintenance;Post Reviewer Note;Temporarily Disable Listing;Update Coordinates;Webcam Photo Taken;Will Attend;Write note;Publish Listing"
$List = List("TypeList","Replace","$TypeList")

$List = List("ImageList","Create",";")
$ImageList = "logArchived;logAttended;logNotFound;logEnable;LogFound;logNeedsArchived;logNeedsMaintenance;logNote;logOwnerMaintenance;logReview;logDisable;logUpdateCoordinates;logWebcam;logWillAttend;logNote;logPublished"
$List = List("ImageList","Replace","$ImageList")

$_sql = "select ldate, ltype, ltext, llogid, lownerid, lby from logsall where lparent = '$d_code' and lIsowner order by llogid desc"
$status = Sqlite("sql",$_sql,"sqlget=yes")

WHILE NOT($_SQLEol)

	$date = DateFormat(SqlToDate((SqlGet("ldate"))))
	$List = List("TypeList","Find",SqlGet("ltype"))
	$Image = List("ImageList","Item","$List")
	$Image = $ImagePreamble + "$Image.gif" + $ImageSuffix + " alt='$d_lType'>"



	IF $_Version >= "8.2.1.62"
		$LogText = BBCode(SqlGet("lText"))
	ELSE
		$LogText = Replace("[b]","<b>",SqlGet("lText"),TRUE)
		$LogText = Replace("[/b]","</b>",$LogText,TRUE)
		$LogText = Replace("[i]","<i>",$LogText,TRUE)
		$LogText = Replace("[/i]","</i>",$LogText,TRUE)
		$LogText = Replace("[:)]",$ImagePreamble + "_smile.gif" + $ImageSuffix,$LogText,TRUE)
		$LogText = Replace("[:D]",$ImagePreamble + "_smile_big.gif" + $ImageSuffix,$LogText,TRUE)
		$LogText = Replace("[8D]",$ImagePreamble + "_cool.gif" + $ImageSuffix,$LogText,TRUE)
		$LogText = Replace("[:I]",$ImagePreamble + "_blush.gif" + $ImageSuffix,$LogText,TRUE)
		$LogText = Replace("[:P]",$ImagePreamble + "_tongue.gif" + $ImageSuffix,$LogText,TRUE)
		$LogText = Replace("[}:)]",$ImagePreamble + "_evil.gif" + $ImageSuffix,$LogText,TRUE)
		$LogText = Replace("[;)]",$ImagePreamble + "_wink.gif" + $ImageSuffix,$LogText,TRUE)
		$LogText = Replace("[:o]",$ImagePreamble + "_clown.gif" + $ImageSuffix,$LogText,TRUE)
		$LogText = Replace("[B]",$ImagePreamble + "_blackeye.gif" + $ImageSuffix,$LogText,TRUE)
		$LogText = Replace("[8]",$ImagePreamble + "_eightball.gif" + $ImageSuffix,$LogText,TRUE)
		$LogText = Replace("[:(]",$ImagePreamble + "_frown.gif" + $ImageSuffix,$LogText,TRUE)
		$LogText = Replace("[8)]",$ImagePreamble + "_shy.gif" + $ImageSuffix,$LogText,TRUE)
		$LogText = Replace("[:O]",$ImagePreamble + "_shock.gif" + $ImageSuffix,$LogText,TRUE)
		$LogText = Replace("[:(!]",$ImagePreamble + "_angry.gif" + $ImageSuffix,$LogText,TRUE)
		$LogText = Replace("[xx(]",$ImagePreamble + "_dead.gif" + $ImageSuffix,$LogText,TRUE)
		$LogText = Replace("[|)]",$ImagePreamble + "_sleepy.gif" + $ImageSuffix,$LogText,TRUE)
		$LogText = Replace("[:X]",$ImagePreamble + "_kisses.gif" + $ImageSuffix,$LogText,TRUE)
		$LogText = Replace("[^]",$ImagePreamble + "_approve.gif" + $ImageSuffix,$LogText,TRUE)
		$LogText = Replace("[V]",$ImagePreamble + "_dissaprove.gif" + $ImageSuffix,$LogText,TRUE)
		$LogText = Replace("[?]",$ImagePreamble + "_question.gif" + $ImageSuffix,$LogText,TRUE)
	ENDIF
	
	$LogText = Replace(Chr(13),"<br>",$LogText,TRUE)

	$html = $html + "<p><a href='http://www.geocaching.com/seek/log.aspx?LID=" + SqlGet("lLogId") + "'>" + "$Image</a> "
	$html = $html + "<b>$date by " + "<a href='http://www.geocaching.com/profile/?id=" + SqlGet("lOwnerId") + "' title='ID#: " + SqlGet("lOwnerId") + "' >" + SqlGet("lby") + "</a></b> <br>"
	$html = $html + $LogText + "</p>"

	SQLNEXT
ENDWHILE

$html = $html + CustomFormat("CloseLeftCol")
$html = $html + CustomFormat("CloseTables")
$html = $html + CustomFormat("CloseDoc")
$_HTML = $html



