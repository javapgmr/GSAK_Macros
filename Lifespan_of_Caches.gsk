# MacDescription = Lifespan of caches in filter
# MacFileName =Lifespan_of_Caches.gsk
# MacAuthor = Kai Team
# MacVersion=2.12

#Determine lifespan (date placed to now or date archived) of caches in the current database, excluding event caches.

$Title="Lifespan of Caches in " + Chr(34) + $_CurrentDatabase + Chr(34) + " Database"
$Form = editform($Form,"Form1","caption",$Title)

$_sql="SELECT Count(*) From Caches WHERE CacheType NOT IN('E','G','I','X','Z') AND $_Where"
$Total=Sqlite("sql",$_sql)

$_sql="SELECT Count(*)||' Archived Caches',' ('||Round(Count(*)/CAST($Total AS Real)*100,2)||'%)',"
$_sql=$_sql + "'Average Lifespan: '||Round(AVG(julianday(LastLog) - julianday(PlacedDate)),1)||' Days',"
$_sql=$_sql + "'('||Round(AVG(julianday(LastLog) - julianday(PlacedDate))/365,1)||' Years)',"
$_sql=$_sql + "'Shortest: '||CAST(Round(Min(julianday(LastLog) - julianday(PlacedDate)),0) AS Integer)||' Day(s)',"
$_sql=$_sql + "'Longest: '||CAST(Round(Max(julianday(LastLog) - julianday(PlacedDate)),0) AS Integer)||' Days',"
$_sql=$_sql + "'('||Round(Max(julianday(LastLog) - julianday(PlacedDate))/365,1)||' Years)' From Caches WHERE Status='X' and CacheType NOT IN('E','G','I','X','Y','Z') AND $_Where"
$ArchivedData=Sqlite("sql",$_sql)
$ArchivedData=Replace("Caches;","Caches ",$ArchivedData)
$ArchivedData=Replace("Days;","Days ",$ArchivedData)
$ArchivedData=Replace(";",$_CrLf,$ArchivedData)

$_sql="SELECT Count(*)||' Active Caches',' ('||Round(Count(*)/CAST($Total AS Real)*100,2)||'%)',"
$_sql=$_sql + "'Average Lifespan: '||Round(AVG(julianday('now') - julianday(PlacedDate)),1)||' Days',"
$_sql=$_sql + "'('||Round(AVG(julianday(LastLog) - julianday(PlacedDate))/365,1)||' Years)',"
$_sql=$_sql + "'Shortest: '||CAST(Round(Min(julianday(LastLog) - julianday(PlacedDate)),0) As Integer)||' Day(s)',"
$_sql=$_sql + "'Longest: '||CAST(Round(Max(julianday(LastLog) - julianday(PlacedDate)),0) AS Integer)||' Days',"
$_sql=$_sql + "'('||Round(Max(julianday(LastLog) - julianday(PlacedDate))/365,1)||' Years)' From Caches WHERE Status<>'X' and CacheType NOT IN('E','G','I','X','Y','Z') AND $_Where"
$ActiveData=Sqlite("sql",$_sql)
$ActiveData=Replace("Caches;","Caches ",$ActiveData)
$ActiveData=Replace("Days;","Days ",$ActiveData)
$ActiveData=Replace(";",$_CrLf,$ActiveData)

$Memo1="Total Caches (excluding Events): " + $Total + $_NewLine  + $_NewLine
IF Left($ArchivedData,1)="0"
    $ArchivedData=Replace($_CrLf,"",$ArchivedData)
ENDIF
$Memo1=$Memo1 + $ArchivedData + $_NewLine + $_NewLine + $ActiveData


WHILE TRUE
    $FormExit = form($Form,"")
    BEGINCASE
        CASE $FormExit = "SystemExit"
        RETURN

        CASE $FormExit = "OK"
        RETURN
    ENDCASE
ENDWHILE

<Data> VarName=$form
#********************************************************************
# Form generated by GSAK form designer on Sat 11-Jun-2011 19:02:51
#********************************************************************

Name = Form1
  Type = Form
  Caption = Lifespan of Caches
  Color = 12632256
  Height = 353
  Top = 359
  Width = 395

Name = OK
  Type = Button
  Height = 25
  Left = 152
  Top = 272
  Width = 75
  Taborder = 8
  Caption = OK

Name = Memo1
  Type = Memo
  Height = 243
  Left = 15
  Readonly = Yes
  Size = 12
  Top = 10
  Width = 355
  Taborder = 9

<enddata>











