#*******************************************
# MacVersion = 1.0
# MacDescription = Caches by placed date
# MacAuthor = bromley
# MacFileName = PQPlaced.gsk
#*******************************************
#****************************************
# Generate PQ placed by dates
#****************************************

VERCHECK Version=7.7.0.0 (Note: this macro requires the latest GSAK 7.7 "patch")

Option Explicit=Yes

Declare Var=$form Type=String
Declare Var=$formexit Type=String
Declare Var=$count Type=Numeric
Declare Var=$dateCount Type=Numeric
Declare Var=$Error Type=String
Declare Var=$FileName Type=String
Declare Var=$From Type=Date
Declare Var=$gdate Type=Date
Declare Var=$gtotal Type=Numeric
Declare Var=$lastcount Type=Numeric
Declare Var=$MacLoc Type=String
Declare Var=$MaxCaches Type=Numeric
DECLARE VAR=$OK Type=Boolean
Declare Var=$out Type=String
Declare Var=$savedate Type=Date
Declare Var=$Sequence Type=String
Declare Var=$Sort Type=String
Declare Var=$SortData Type=String
Declare Var=$status Type=String
Declare Var=$temp Type=String
Declare Var=$ThisPQ Type=Numeric
Declare Var=$ThisPQStr Type=String
Declare Var=$ThisRecord Type=Numeric
Declare Var=$tick Type=String
Declare Var=$MaxDistance Type=Numeric
DECLARE Var=$vDistance Type=String
DECLARE Var=$vCaches Type=String


$MacLoc=$_Install + "\Macros\PQPlaced.xml"
IF FileExists($MacLoc)
	MacSettings Type=R
ELSE
# change this number as requried
$vCaches = "490"
$vDistance = "150"
ENDIF

$formexit = form($form, "")
IF $ok
GOSUB Name=Main
ENDIF


BEGINSUB Name=Main
$MaxDistance = Val($vDistance)
$MaxCaches = Val($vCaches)	
	
MACSettings Type=S  Vars=vCaches,vDistance
# save current sort sequence
$Sort = $_SortBy

# temp folder to generate HTML file
$temp = $_Install + "\temp"

# check box data to help see where we are up to
$tick = "<input type=checkbox value=Yes name=""CHECK_BOX_1"">"

# generate the HTML header info
GoSub Name=Header

# Fitler out archived
Mfilter where=status <> 'X' AND Distance <= $MaxDistance

# if you would prefer to use a saved filter for your selection uncomment and change the next line
# Filter Name=MyFilter

SQLSORT Orderby=PlacedDate

$count = 0
$dateCount = 1
$From = $d_PlacedDate
$savedate = $d_PlacedDate
$ThisPQ = 0
$ThisRecord = 0
$lastcount = 0


$status = sqlite("sql",$_SqlGrid, "sqlget=yes")

WHILE NOT($_SQLEol)
  $ThisRecord = $ThisRecord + 1
  $count = $count + 1
  
  # only show status every 100 records
  IF frac($ThisRecord/100) = 0
    $status = "Record: " + "$ThisRecord" + " of " + "$_Count"
    ShowStatus msg="$status"
  ENDIF
  

  IF SqlToDate(SqlGet("placedDate")) <> $savedate
    $gdate = $savedate
    $gtotal = $count - 1
    $savedate = SqlToDate(SqlGet("placedDate"))
  ENDIF


  # if too many caches, use the previous date
  IF $count > $MaxCaches
    $ThisPQ = $ThisPQ + 1
    $ThisPQStr = Right("00000" + NumToStr($ThisPQ), 2)
    $out = $out + "<tr class=row><td>" + $ThisPQStr + "</td><td>" + DateFormat($from) + "</td><td>" + DateFormat($gdate) + "</td><td> $gtotal</td>" + "<td>$tick</td></tr>" +  $_NewLine
    $from = $gdate + 1
    $count = $count - $gtotal
  ENDIF
  
  SQLNEXT
ENDWHILE

# generate last from and to if total recs not a multiple of $maxCaches
IF $count > 0
    $ThisPQ = $ThisPQ + 1
    $ThisPQStr = Right("00000" + NumToStr($ThisPQ), 2)
    $out = $out + "<tr class=row><td>" + $ThisPQStr + "</td><td>" + DateFormat($from) + "</td><td>Maximum date</td><td>$count</td>" + "<td>$tick</td></tr>" +  $_NewLine
ENDIF

CANCELFILTER

# restore previous sort sequnce
$SortData = Extract($Sort, "=",1)
$Sequence = Extract($Sort, "=",2)
SORT By=$SortData Sequence=$Sequence 


GoTo position=Top

#close out HTML
$out = $out + "</table></body></html>"

# generate file and display
$FileName = $temp + "\placedpq.htm"
$Error = PutFile($FileName,$out)
OpenFile file="$FileName"
ENDSUB


BeginSub Name=Header
  $out = "<html>" + $_NewLine
  $out = $out + "<style type='text/css'>" + $_NewLine
  $out = $out + "<!--" + $_NewLine
  $out = $out + ".Body{font-family:'Arial'}" + $_NewLine
  $out = $out + ".TableHeader{background-color:#CCCCFF;text-align: center}" + $_NewLine
  $out = $out + ".Item{color:blue;font-weight:bold}" + $_NewLine
  $out = $out + ".row{background-color: #FEF4D8;vertical-align:top}" + $_NewLine
  $out = $out + "-->" + $_NewLine
  $out = $out + "</style>" + $_NewLine
  $out = $out + "<body class=body>" + $_NewLine
  $out = $out + "<table border='1' summary=''>"
  $out = $out + "<tr class=TableHeader><td align='center' colspan=5>Placed by date PQ Placed </td></tr>" + $_NewLine
  $out = $out + "<tr class=TableHeader><td>PQ</td><td>From</td><td>To</td><td>Count</td><td>Done </td></tr>" +  $_NewLine
EndSub

<Data> VarName=$form
#********************************************************************
# Form generated by GSAK form designer on Sat 06-Dec-2008 22:22:48
#********************************************************************

Name = Form1
  Type = Form
  Caption = PQ Placed Macro
  Height = 179
  Width = 265

Name = Label1
  Type = Label
  Height = 16
  Left = 6
  Size = 10
  Style = bold
  Top = 32
  Width = 133
  Caption = Maximum Distance:

Name = Label2
  Type = Label
  Height = 16
  Left = 6
  Size = 10
  Style = bold
  Top = 79
  Width = 169
  Caption = Maximum No. of Caches:

Name = vDistance
  Type = Edit
  Height = 21
  Left = 184
  Top = 28
  Width = 65
  Taborder = 8

Name = vCaches
  Type = Edit
  Height = 21
  Left = 184
  Top = 75
  Width = 65
  Taborder = 9

Name = Label3
  Type = Label
  Height = 20
  Left = 60
  Size = 12
  Style = bold
  Top = 4
  Width = 153
  Caption = Caculate PQ Dates

Name = OK
  Type = Button
  Enter = Yes
  Height = 25
  Left = 20
  Top = 114
  Width = 75
  Taborder = 10
  Caption = OK

Name = vCancel
  Type = Button
  Height = 25
  Left = 132
  Top = 114
  Width = 75
  Taborder = 11
  Caption = Cancel

<enddata>
