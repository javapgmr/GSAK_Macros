#*******************************************
# MacVersion = 1.1.0
# MacDescription = Mark possible corrupted records with a user flag and prepare report on caches with errors.
# MacAuthor = javapgmr
# MacFileName = DBCheckSql.gsk
# MacUrl = http://gsak.net/board/index.php?showtopic=8703
#*******************************************
#
# original errors checked for are an invalid url on GC records and blank logs in log file.
#
# 1.0.0  - Original Releasee
# 1.1.0  - Add timed popup window notifing user of clean condition when database has no errors.

VERCHECK Version=7.3.0.0 (Note: this macro requires the latest GSAK "patch" - please see the 7.2.3 Updates thread here: http://gsak.net/board/index.php?showforum=6)
GOSUB Name=DECLARE

#-------------------------------------
CancelFilter

$OutFileName = $_Install + "\html\dbchecksql.html"
UserFlag Type=clear Range=All
$DBfile = ":memory:"

#Error criteria can be added here as new error conditions are detected.
$WhereClause = "Code like 'GC%' and Url not like '%http://www.geocaching.com/seek/cache_details.aspx%'"
$WhereClause2 = "(ltext is null)  "

# Create the caches and waypoints tables within the Database. If the database doesn't exist yet it will automatically be created. Same with the Tables - they will be overwritten if they exist or created if they don't.
# First check for error criteria and then save the count
$_sql = "SELECT Code FROM cachesAll where $WhereClause"
$TmpS = SQLite("sql",$_sql,"")
$Count = $_sqlRows

# First check for error criteria and then save the count
$_sql = "SELECT lparent FROM logMemo where $WhereClause2"
$TmpS2= SQLite("sql",$_sql,"")
$Count2 = $_sqlRows

# If error conditions have been detected, publish them and then mark them in the database.
IF ($Count > 0 .or. $Count2 > 0)
    IF ($Count > 0)
	  $_sql = "SELECT Code,SmartName, Name FROM cachesAll where $WhereClause Order by Code"
      $TmpSa = SQLite("sql",$_sql,"headings=yes")
      $status = CodeMatch($TmpS," ","U")
    ENDIF
    IF ($Count2 > 0)
	  $_sql = "SELECT distinct lparent as Code, SmartName, Name FROM logMemo,cachesAll on logs.lparent = cachesAll.code where $WhereClause2 Order by lParent"
      $TmpS2a= SQLite("sql",$_sql,"headings=yes")
      $status = CodeMatch($TmpS2," ","U")
    ENDIF

    $html=RegExReplace("(?s)</body>.*?</html>",SqlToHtml($TmpSa,"Caches containing invalid Url's.",""),"")
    $html=$html + "<br clear='all'><br><br>"
    $html=$html + RegExReplace("(?s)<!DOCTYPE.*?<body>",SqlToHtml($TmpS2a,"Caches with invalid logs.",""),"")

    $Status = PutFile($OutFileName,$html)
    IF Left($Status,7) = "*Error*"
       Pause Msg="$Status"
       Cancel
    ENDIF
    WEB URL=$OutFileName
ELSE
  $MsgTxt = "No errors detected."
  $MsgForm = Replace("@msg@",$MsgTxt,$MsgForm,TRUE)
  $FormExit = form($MsgForm,"")

ENDIF


#*******************************************************
#   Variable declarations for
#   DBCheckSQL.gsk
#
#   Generated 06/20/2008 5:08:40 PM on GSAKVariables.gsk Rev V0.20 B15
#
#*******************************************************

BEGINSUB Name=Declare
Option Explicit=Yes
DECLARE Var=$Count Type=Numeric
DECLARE Var=$Count2 Type=Numeric
DECLARE Var=$DBfile Type=String
DECLARE Var=$FormExit Type=String
DECLARE Var=$html Type=String
DECLARE Var=$MsgForm Type=String
DECLARE Var=$MsgTxt Type=String
DECLARE Var=$OutFileName Type=String
DECLARE Var=$sqlinputfile Type=String
DECLARE Var=$status Type=String
DECLARE Var=$TmpS Type=String
DECLARE Var=$TmpS2 Type=String
DECLARE Var=$TmpS2a Type=String
DECLARE Var=$TmpSa Type=String
DECLARE Var=$WhereClause Type=String
DECLARE Var=$WhereClause2 Type=String
ENDSUB

<Data> VarName=$MsgForm
#********************************************************************
# Form generated by GSAK form designer on Sat 30-Apr-2011 20:09:20
#********************************************************************

Name = Form1
  Type = Form
  Caption = Message
  Delay = 5
  Height = 98
  Width = 327

Name = MsgTxtf
  Type = Label
  Font = Arial
  Height = 22
  Left = 4
  Size = 14
  Style = bold
  Top = 4
  Width = 305
  Caption = @msg@

Name = Button1
  Type = Button
  Enter = Yes
  Height = 24
  Left = 120
  Top = 33
  Width = 75
  Taborder = 8
  Caption = OK

<enddata>


