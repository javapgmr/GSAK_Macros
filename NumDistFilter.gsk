#*******************************************
# MacDescription = Filter for a number and distance
# MacFileName = NumDistFilter.gsk
# MacAuthor = Kai Team
# MacVersion=1.1
# MacUrl=
#*******************************************

#Present the user with a form to choose the distance and number of caches
WHILE TRUE
    $FormExit = form($Form,"")
    BEGINCASE
        CASE $FormExit = "SystemExit"
        CANCEL Msg="Macro Cancelled"

        CASE $FormExit = "OK"
        GOSUB Name=Run
        BREAK

        CASE $FormExit = "Cancel"
        CANCEL Msg="Macro Cancelled"
    ENDCASE
ENDWHILE

BEGINSUB Name=Run
    #set the counter variable to zero
    $x=0
    #get the latitude and longitude of the currently selected cache
    $LatLon=$d_Latitude + " " + $d_Longitude
    #center on the coordinates of the currently selected cache
    CENTRE LOCATION="$d_Name" COORDINATES=$LatLon
    #clear all macro flags
    MACROFLAG Type=Clear Range=All
    #sort by distance from the center point (current cache) - also moves the current cache to the top
    SORT by="distance"
    #Make sure the pointer is on the center point cache (at the top)
    GOTO Position=Top
    #Set macro flags for the number of caches chosen
    MACROFLAG Type=SET Range=$NumCaches
    #Filter for the distance specified
    MFILTER Expression=$d_Distance<=Val($Distance)
    #Filter on the macroflag=set to get the number, if it's less than the distance
    MFILTER Expression=$d_MacroFlag Join=AND
ENDSUB

#data variable for the form
<Data> VarName=$form
#********************************************************************
# Form generated by GSAK form designer on Sun 19-Jul-2009 08:29:20
#********************************************************************

Name = Form1
  Type = Form
  Caption = Filter by number & distance
  Height = 165
  Width = 286

Name = OK
  Type = Button
  Height = 25
  Left = 41
  Top = 92
  Width = 75
  Taborder = 8
  Caption = OK

Name = Cancel
  Type = Button
  Height = 25
  Left = 161
  Top = 92
  Width = 75
  Taborder = 9
  Caption = Cancel

Name = Label1
  Type = Label
  Height = 16
  Left = 18
  Size = 10
  Top = 20
  Width = 174
  Caption = Number of Caches to Include:

Name = NumCaches
  Type = Edit
  Height = 21
  Left = 202
  Top = 18
  Width = 49
  Taborder = 10

Name = Label2
  Type = Label
  Height = 16
  Left = 18
  Size = 10
  Top = 54
  Width = 178
  Caption = Distance less than or equal to:

Name = Distance
  Type = Edit
  Height = 21
  Left = 202
  Top = 52
  Width = 49
  Taborder = 11

<enddata>

