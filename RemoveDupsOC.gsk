#***********************
# MacDescription =Remove Opencaching Waypoints with Duplicate Coordinates
# MacFileName = RemoveDupsOC.gsk
# MacAuthor = Kai Team, Cache U Nuts, Qwerty
# MacVersion=4.2
# MacURL=http://gsak.net/board/index.php?showtopic=13178&view=findpost&p=87981
#**********************

#Version Check
VERCHECK version=7.0.0 This macro requires GSAK version 7 or later.  Please update GSAK to run this macro (go to http://gsak.net)

#Initialize variables
$ThisRecord=0
$OrigSortOrder = $_SortBy
$Radiobutton1=TRUE
$Checkbox1=TRUE

#Save existing UserData2 information for later restoration
DATASAVE Data=$d_User2

#Clear all macroflags
MACROFLAG Type=Clear Range=All

SPLITSCREEN Display=Off

SORT By="Natural"

#Copy Lattitude and Longitdue to UserData2
GOTO Position=Top
WHILE not($_EoL)
    # Show status of macro (every 10 records)
    $ThisRecord = $ThisRecord + 1
    IF frac($ThisRecord/10) = 0
         $status = "Gathering Coordinates: " + "$ThisRecord" + " of " + "$_Count"
         ShowStatus msg="$status" Width=250
    ENDIF
    $d_User2=$d_Latitude + " " + $d_Longitude
    GOTO Position=next
ENDWHILE

#Sort by UserData2,code
SORT By="user2;code" Sequence=D

#Identify and flag waypoints with duplicate coordinates
GOTO Position=Top
$ThisRecord=0
WHILE NOT($_EoL)
    # Show status of macro (every 10 records)
    $ThisRecord = $ThisRecord + 1
    IF frac($ThisRecord/10) = 0
        $status = "Comparing Coordinates: " + "$ThisRecord" + " of " + "$_Count"
        ShowStatus msg="$status" Width=350
    ENDIF
   $Control=$d_User2
   GOTO Position=Next
   IF $_EoL
       BREAK
   ENDIF
   $Comparison=$d_User2
   $data = $Comparison + ";" + $Control
   $DistanceInFeet = Val(GCalc($data,"CalcDistance")) * 5280
   IF ($DistanceInFeet < 5) AND Left($d_Code,2)="OC"
      $d_MacroFlag=True
   ENDIF
ENDWHILE

#Hide status meesage
SHOWSTATUS msg="Hide status" Width=350 Display=Off

#Filter for and delete all flagged waypoints
SPEEDMODE Status=Off
MFILTER IF=$d_MacroFlag
IF $_FilterCount>0
    WHILE TRUE
    $FormExit = form($ConfirmDelete,"")
    BEGINCASE
        CASE $FormExit = "SystemExit"
            GOSUB Name=CleanUp
            CANCEL Msg="Macro Cancelled"

        CASE $FormExit = "OK"
            SPEEDMODE Status=On
            GOSUB Name=DELETE
            GOSUB Name=Cleanup
            BREAK

        CASE $FormExit="FilterOnly"
            $SavedFilter = SaveFilter()
            GOSUB Name=CleanUp
            $RestoreFilter=RestoreFilter($SavedFilter,FALSE)
            BREAK

        CASE $FormExit = "Cancel"
            GOSUB Name=CleanUp
            CANCEL Msg="Macro Cancelled"
    ENDCASE
    ENDWHILE
ELSE
    GOSUB Name=DELETE
ENDIF

BEGINSUB Name=DELETE
IF $_FilterCount>0
    $status = "Deleting " + "$_FilterCount" + " Duplicate Waypoints." + $_NewLine + "Please wait for the macro to finish."
        ShowStatus msg="$status" Width=350
    MACROSET Dialog=Delete VarName=$Delete
    DELETE Settings=<macro>
ELSE
   $Message="There are no duplicate waypoints to delete." + $_NewLine +  $_NewLine + "Please click 'Continue' and wait a few seconds for the macro to clean up after itself."
   PAUSE Msg=$Message
ENDIF
GOSUB Name=Cleanup
ENDSUB

BEGINSUB Name=CleanUp
    CANCELFILTER
    #Restore UserData2 & User's Original Sort Order
    DATARESTORE Data=$d_User2
    $OrigSort = Extract($OrigSortOrder, "=",1)
    $OrigSequence = Extract($OrigSortOrder, "=",2)
    SORT By=$OrigSort Sequence=$OrigSequence
    SPLITSCREEN Display=On
ENDSUB

<data> VarName=$Delete
[TfmDelete]
cbxStop.Checked=False
rbtFilter.Checked=True
rbtFlagged.Checked=False
rbtOnly.Checked=False
<enddata>

<Data> VarName=$ConfirmDelete
#********************************************************************
# Form generated by GSAK form designer on Sat 27-Dec-2008 15:36:44
#********************************************************************

Name = Form2
  Type = Form
  Caption = Confirm Deletion!
  Height = 151
  Width = 372

Name = Label1
  Type = Label
  Color = 4194368
  Height = 24
  Left = 35
  Size = 14
  Top = 15
  Width = 294
  Caption = Delete or filter  the cache(s) shown:

Name = Cancel
  Type = Button
  Height = 25
  Left = 250
  Top = 67
  Width = 75
  Taborder = 0
  Caption = Cancel

Name = Memo1
  Type = Memo
  Color = 255
  Height = 36
  Left = 149
  Top = 60
  Width = 83
  Taborder = 1

Name = OK
  Type = Button
  Height = 25
  Left = 151
  Top = 66
  Width = 79
  Taborder = 10
  Caption = DELETE

Name = FilterOnly
  Type = Button
  Height = 25
  Left = 43
  Top = 67
  Width = 75
  Taborder = 9
  Caption = Filter Only

<enddata>

