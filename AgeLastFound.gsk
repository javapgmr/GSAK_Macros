#*******************************************
# MacDescription = Report age since last find of available caches
# MacFileName = AgeLastFound.gsk
# MacAuthor = Kai Team
# MacVersion=1.1
# MacUrl=
#*******************************************

$Spaces="          "
WHILE TRUE
    $FormExit = form($Form,"")
    BEGINCASE 
        CASE $FormExit = "SystemExit"
        CANCEL Msg="Macro Cancelled"

        CASE $FormExit = "OK"
        GOSUB Name=Run
        BREAK 

        CASE $FormExit = "Cancel"
        CANCEL Msg="Macro Cancelled"
    ENDCASE 
ENDWHILE

BEGINSUB Name=Run
    $Out="Code" + ";" + "Name" + ";" + "Type" + ";" + "Dif" + ";" + "Ter" + ";"
    $Out=$Out + "Placed"  + ";" + "Last Found"  + ";" + "Days"+ $_Newline
    MFILTER Expression=Not($d_Found or $d_Archived or $d_TempDisabled)
    GOTO Position=Top
    WHILE NOT($_EOL)
        IF $d_LastFoundDate=[00000000]
            $Date=$d_PlacedDate
            $DateString="Not Found"
        ELSE
            $Date=$d_LastFoundDate
            $DateString=DateFormat($d_LastFoundDate)
        ENDIF
        $Days=NumToStr(DateDiff($Date,$_Today))
        $Out=$Out + $d_Code + ";" + Replace(";"," ",$d_Name,TRUE) + ";" + $d_CacheType  + ";"
        $Out=$Out + NumToStr($d_Difficulty) + ";" + NumToStr($d_Terrain) + ";" 
        $Out=$Out + DateFormat($d_PlacedDate) + ";" + $DateString  + ";" 
        $Out=$Out + $Days + $_Newline
        BEGINCASE
            CASE $Combobox1="User Data"
                $d_UserData=Left($spaces,5-Len($Days)) + $Days
            CASE $Combobox1="User Data 2"
                $d_User2=Left($spaces,5-Len($Days)) + $Days
        ENDCASE
        GOTO Position=Next
    ENDWHILE
    $Output=SqlToHtml($Out,"Days since last found (or placed, if not found)","Y") 
ENDSUB

<Data> VarName=$form
#********************************************************************
# Form generated by GSAK form designer on Sun 11-Jan-2009 16:21:48
#********************************************************************

Name = Form1
  Type = Form
  Caption = Age Since Last Find (or Placed if not found)
  Height = 166
  Width = 338

Name = Label1
  Type = Label
  Font = Arial
  Height = 16
  Left = 29
  Size = 10
  Top = 10
  Width = 271
  Caption = Choose the GSAK field for the number of days:

Name = Combobox1
  Type = Combobox
  Height = 21
  Left = 78
  Top = 49
  Values = None;User Data;User Data 2
  Width = 173
  Taborder = 8

Name = OK
  Type = Button
  Height = 25
  Left = 64
  Top = 90
  Width = 75
  Taborder = 9
  Caption = OK

Name = Cancel
  Type = Button
  Height = 25
  Left = 190
  Top = 90
  Width = 75
  Taborder = 10
  Caption = Cancel

Name = Label2
  Type = Label
  Height = 13
  Left = 70
  Top = 26
  Width = 189
  Caption = (Warning: This will replace existing data)

<enddata>

