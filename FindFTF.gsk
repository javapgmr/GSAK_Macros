#*******************************************
# MacDescription = Filter for possible "first to find"
# MacFileName = FindFTF.gsk
# MacAuthor = Kai Team
# MacVersion=2.11
# MacUrl=http://gsak.net/board/index.php?showtopic=12907&view=findpost&p=85710
#*******************************************
# Version 2.1 - removed cache types where FTF is irrelevant
    #Modified code to trap errors when there are no found caches in the DB, or when found logs are missing from caches marked found
    #Ignores cache types where FTF is irrelevant (CITO, Event, Benchmark, Maze Exhinit, Waymark, Mega Event)
    #Updated version check to current release version of GSAK
    #Updated count count by appending a ; to the list and removing +1 from the $Count
    #Add a CRLF to the first query result before concatenation if the first query is not empty.
#Version 2.11 - Conditionally remove CRLF added above if second query result is empty
#TIMER Status=On

VERCHECK Version=7.7.3.53 (please update GSAK to version 7.7.3.53 or later to run this macro - see http://gsak.net)
$PutUserSortCB=FALSE
CANCELFILTER
MACROFLAG Type=Clear Range=All
$Edit1="5"
WHILE TRUE
    $FormExit = form($Form,"")
    BEGINCASE
        CASE $FormExit = "SystemExit"
        CANCEL Msg="Macro Cancelled"

        CASE $FormExit = "OK"
        GOSUB Name=Run
        BREAK

        CASE $FormExit = "Cancel"
        CANCEL Msg="Macro Cancelled"
    ENDCASE
ENDWHILE

BEGINSUB Name=Run
    $Data=""
    $DateLimit=Val($Edit1)
    $_sql="SELECT Code from Caches WHERE Found"
    $FoundCodes=Sqlite("sql",$_sql)
    $FoundCodes="'" + Replace($_CrLf,"','",$FoundCodes) + "'"

    $_sql="Select lParent from Logs WHERE ltype='Publish Listing' AND lParent IN ($FoundCodes)"
    $PublishedCaches=Sqlite("sql",$_sql)
    $PublishedCaches="'" + Replace($_CrLf,"','",$PublishedCaches) + "'"

    $_sql="Select lParent,(Select PlacedDate from Caches WHERE Code=lParent) as Placed "
    $_sql=$_sql + "from Logs WHERE lParent NOT IN ($PublishedCaches) AND lParent IN ($FoundCodes) GROUP BY lparent"
    $Data=Sqlite("sql",$_sql)

    IF $Data<>""
        $Data=$Data + $_CrLf
    ENDIF

    $_sql="Select lParent,lDate from Logs WHERE ltype='Publish Listing' AND lParent IN ($FoundCodes)"
    $PublishedData=Sqlite("sql",$_sql)
    $Data=$Data + $PublishedData
    $Data=Replace($_CrLf,";",$Data)

    IF $PublishedData<>""
        $Data=$Data + ";"
    ENDIF

    $Count=RegExCount(";",$Data)
    $b=$count/2

    MFILTER Where=Found AND CacheType Not IN ('C','E','G','X','Y','Z')
    IF $_FilterCount=0
        CANCEL Msg=There are no found caches in this database.  Macro canceled.
    ENDIF
    $x=1
    WHILE $x<=$Count
        $a=($x+1)/2
        $Status="Now checking cache $a of $b...  Please wait..."
        SHOWSTATUS Msg=$Status Width=350

        $Code=Extract($Data,";",$x)
        $x=$x+1
        $PubDate=Extract($Data,";",$x)

        $_sql="SELECT lDate from Logs WHERE lParent='$Code' and lIsOwner=1 and g_foundlog(ltype)"
        $FoundDate=Sqlite("sql",$_sql)

        IF $FoundDate<>""
            $DateDiff=DateDiff(SqlToDate($PubDate),SqlToDate($FoundDate))
        ELSE
            CANCEL Msg=Some found caches are missing your found logs. Please load the My Finds Pocket Query from geocaching.com and try again.
        ENDIF

        TRANSACTION Action=Begin
        IF DateDiff(SqlToDate($PubDate),SqlToDate($FoundDate))<=$DateLimit AND Seek($Code)
            $d_MacroFlag=TRUE
            $d_MacroSort = NumToStr($DateDiff+1000)  # add 1000 to insure 4 digit positive result
            IF $PutUserSortCB=TRUE
                $d_UserSort=$DateDiff
            ENDIF
        ENDIF
        TRANSACTION Action=End

        $x=$x+1
    ENDWHILE

    MFILTER WHERE=MACROFLAG=1 Join=AND
    SQLSORT OrderBy=MacroSort

    IF $_FilterCount>0
        MSGOK Msg=Possible FTF candidates shown in grid.
    ELSE
        MSGOK Msg=No FTF candidates where found in this database.
    ENDIF
ENDSUB
#TIMER Status=Off

<Data> VarName=$form
#********************************************************************
# Form generated by GSAK form designer on Sat 30-Apr-2011 19:17:45
#********************************************************************

Name = Form1
  Type = Form
  Caption = Find FTF's
  Height = 149
  Width = 267

Name = OK
  Type = Button
  Height = 25
  Left = 38
  Top = 72
  Width = 75
  Taborder = 8
  Caption = OK

Name = Cancel
  Type = Button
  Height = 25
  Left = 146
  Top = 72
  Width = 75
  Taborder = 9
  Caption = Cancel

Name = Label1
  Type = Label
  Height = 16
  Left = 19
  Size = 10
  Top = 18
  Width = 179
  Caption = Enter the search limit (in days):

Name = Edit1
  Type = Edit
  Height = 21
  Left = 201
  Top = 16
  Width = 31
  Taborder = 10

Name = PutUserSortCB
  Type = Checkbox
  Fontsize = 10
  Height = 17
  Left = 18
  Top = 42
  Width = 223
  Taborder = 11
  Caption = Put date difference in user sort

<enddata>




