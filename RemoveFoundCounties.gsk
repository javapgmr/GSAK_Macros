#*******************************************
# MacDescription = Remove caches from counties with found caches
# MacFileName = RemoveFoundCounties.gsk
# MacAuthor = Kai Team
# MacVersion=3.12
# MacUrl=http://gsak.net/board/index.php?showtopic=9997&view=findpost&p=64778
#*******************************************
VERCHECK Version=7.7.0.109 (please update GSAK to version 7.7.0.109 or later to run this macro - see http://gsak.net)

$DBs=Sysinfo("databases")
$DBs="Select your found database" + ";" + $DBs

WHILE TRUE
    $FormExit = form($Form,"")
    BEGINCASE
        CASE $Formexit="FoundDB"
        $FoundDatabase=$_DbPath + "\" + $FoundDB + "\" + "Sqlite.db3"
        $_sql="ATTACH '$FoundDatabase' AS Found"
        $SQLresult=Sqlite("sql",$_sql)
        $_sql="SELECT DISTINCT State FROM found.caches WHERE Found=1 AND County IS NOT NULL AND trim(county) <> '' AND state IS NOT NULL AND trim(state) <> ''"
        $States=Sqlite("sql",$_sql)
        $States=Replace($_NewLine,";",$States,TRUE) + ";"
        $Form=Editform($Form,"Combobox3","Values",$States)

        CASE $FormExit = "OK"
        GOSUB name=Run
        BREAK

        CASE $FormExit = "SystemExit"
        CANCEL Msg="Macro Cancelled"

        CASE $FormExit = "Cancel"
        CANCEL Msg="Macro Cancelled"
    ENDCASE
ENDWHILE

BEGINSUB Name=Run
    MACROFLAG Type=Clear range=all
    $_sql="SELECT code FROM caches WHERE county IN (select DISTINCT County FROM found.caches WHERE State = '$Combobox3' and Found=1)"
    $match=Sqlite("sql",$_sql)
    $result = CodeMatch($match,"$_Crlf","M")

    IF $_FilterActive
        MFILTER Expression=Not($d_MacroFlag) AND State=$Combobox3 AND NOT(IsEmpty($d_county)) AND NOT(IsEmpty($d_State)) JOIN=AND
    ELSE
        MFILTER Expression=Not($d_MacroFlag) AND State=$Combobox3 AND NOT(IsEmpty($d_county)) AND NOT(IsEmpty($d_State))
    ENDIF

    IF $_FilterCount = 0
        MSGOK msg="No matching records found"
    ENDIF
    $_sql="DETACH Found"
    $SQLresult=Sqlite("sql",$_sql)
ENDSUB

<Data> VarName=$form
#********************************************************************
# Form generated by GSAK form designer on Sat 27-Mar-2010 15:04:15
#********************************************************************

Name = Form1
  Type = Form
  Caption = Filter Out Found Counties in Current Database or Filter
  Height = 167
  Width = 529

Name = Label1
  Type = Label
  Height = 16
  Left = 12
  Size = 10
  Top = 16
  Width = 312
  Caption = Select the database that contains your found caches:

Name = FoundDB
  Type = Combobox
  Exitonchange = Yes
  Height = 21
  Left = 333
  Top = 14
  Values = $DBs
  Width = 165
  Taborder = 1

Name = OK
  Type = Button
  Height = 25
  Left = 97
  Top = 86
  Width = 75
  Taborder = 3
  Caption = Apply

Name = Cancel
  Type = Button
  Height = 25
  Left = 319
  Top = 86
  Width = 75
  Taborder = 4
  Caption = Cancel

Name = Label3
  Type = Label
  Height = 16
  Left = 12
  Size = 10
  Top = 49
  Width = 307
  Caption = Select the State whose counties should be included:

Name = Combobox3
  Type = Combobox
  Height = 21
  Left = 333
  Top = 47
  Width = 165
  Taborder = 2

<enddata>





