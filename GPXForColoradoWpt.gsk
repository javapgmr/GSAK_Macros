#***************************************
#   Convert GPX file to Colorado Waypoint format
#   v0.2 modified  1/25/08
#   Lignumaqua
#
#***************************************

# MacVersion = 0.2
# MacDescription = Convert GPX File to Colorado Waypoint format
# MacAuthor = Lignumaqua
# MacFileName = GPXForColoradoWpt.gsk

VERCHECK Version=7.2.0.126 (You can find the latest version of GSAK in the forums at http://support.gsak.net/.)

WHILE TRUE
		$result = Form($form,"")
		# Reset the screen position of the menu form to where the user left it
  	$Form = EditForm($Form,"Form1","Top",$_FormTop)
  	$Form = EditForm($Form,"Form1","Left",$_FormLeft)
  	
  	BEGINCASE #Buttons
  		CASE $Cancel
    		CANCEL
			CASE $OK
				BREAK
	    OTHERWISE 
  			CANCEL
  	ENDCASE	
ENDWHILE # Form Loop

$flag = FALSE			# Flag that we've found a <name> tag
$total = 0
$cacheGPX = ""	# storage for each cache - doing it this way rather than with one big string speeds things up a lot
$GPX = ""				# Final output 
$count = 0

# Get file and strip out the <groundspeak> tags 


$GPX = GetFile($file)
IF Left($GPX,7) = "*Error*"
	PAUSE Msg="$GPX"
	CANCEL
ENDIF

$GPX = RegExReplace("(?s)<groundspeak:cache.*?</groundspeak:cache.*?>",$GPX,"")
$GPX = RegExReplace("<groundspeak.*",$GPX,"")
$GPX = RegExReplace("</groundspeak.*",$GPX,"")
$GPX = RegExReplace("<type>.*",$GPX,"")	
$GPX = RegExReplace("(?s)<gpx.*?>",$GPX,"$gpxheader")	

$file = Replace(".gpx","_wpt.gpx",$file,TRUE)

$result = PutFile($file, $GPX)
IF Left($result, 7) = "*Error*"
	# If theres any error abort the macro
	CANCEL Msg="Unable to write to $file!"
ENDIF

$GPX = ""
FILEREAD file=$file
	IF (RegExCount("<wpt",$line))>0
		$flag = TRUE
	ENDIF
	
	IF (RegExCount("<name>",$line)) >0
		$name = RegExSub("<name>(.*?)</name>",$line,1,1)
		$total = $total + 1
		SHOWSTATUS msg="Converting cache #: $total, $name" Width=350
	ENDIF
	
	IF (RegExCount("<desc>",$line))>0 AND $flag
		$linetemp = $line
		$line = RegExReplace("<desc>",$line,"<cmt>")	
		$line = RegExReplace("</desc>",$line,"</cmt>")
		$line = $line + $_NewLine + $linetemp
	ENDIF
		
	IF (RegExCount("</wpt>",$line))>0
		$line = $wptfooter + $line + $_NewLine
	ENDIF
	
	IF Len($line) > 0
		$cacheGPX = $cacheGPX + $line + $_NewLine
	ENDIF
	
ENDREAD

$GPX = $GPX + $cacheGPX


$result = PutFile($file, $GPX)
	IF Left($result, 7) = "*Error*"
		# If theres any error abort the macro
		CANCEL Msg="Unable to write to $file!"
	ENDIF

MSGOK msg="GPX file converted"

<Data> VarName=$gpxheader
<gpx xmlns="http://www.topografix.com/GPX/1/1" creator="GSAK" version="1.1" xmlns:xsi="http://www.w3.org/2001/XMLSchema-instance" xsi:schemaLocation="http://www.garmin.com/xmlschemas/GpxExtensions/v3 http://www.garmin.com/xmlschemas/GpxExtensions/v3/GpxExtensionsv3.xsd http://www.topografix.com/GPX/1/1 http://www.topografix.com/GPX/1/1/gpx.xsd">
<enddata>

<Data> VarName=$wptfooter
<extensions>
<gpxx:WaypointExtension xmlns:gpxx="http://www.garmin.com/xmlschemas/GpxExtensions/v3">
<gpxx:DisplayMode>SymbolAndName</gpxx:DisplayMode>
</gpxx:WaypointExtension>
</extensions>
<enddata>

<Data> VarName=$form
#********************************************************************
# Form generated by GSAK form designer on Fri 25-Jan-2008 22:24:08
#********************************************************************

Name = Form1
  Type = Form
  Height = 250
  Width = 273

Name = Label1
  Type = Label
  Color = 16711680
  Height = 18
  Left = 10
  Size = 11
  Style = bold
  Top = 20
  Width = 245
  Caption = Edit GPX for Colorado Waypoint

Name = File
  Type = File
  Height = 21
  Left = 52
  Top = 80
  Width = 161
  Taborder = 8

Name = Label2
  Type = Label
  Height = 13
  Left = 72
  Top = 60
  Width = 122
  Caption = Select GPX file to convert

Name = Cancel
  Type = Button
  Height = 25
  Left = 35
  Top = 150
  Width = 75
  Taborder = 9
  Caption = Cancel

Name = OK
  Type = Button
  Height = 25
  Left = 155
  Top = 150
  Width = 75
  Taborder = 10
  Caption = OK

<enddata>

