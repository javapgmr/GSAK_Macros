#*******************************************
# MacDescription = Filter caches for two cachers based on dates
# MacFileName = FilterByFindOrderSQL.gsk
# MacAuthor = Bronstein (based on Kai Team's FilterByFindOrder.gsk)
# MacVersion=1.1
# MacUrl=
#*******************************************

ShowStatus msg="Please wait, SQL will be initialized ..."
#Initialize SQLite with the needed database variables from the log table
$SQLdatabase = ":memory:"
$ReturnStr=DBtoSQLite("logs","lParent,lOwnerID,lDate,lType",$SQLdatabase)

#Now delete all the log entries which have nothing to do with found
$_sql = "Delete from logs where (lType<>'Found it') and (lType<>'Attended') and (lType<>'Webcam Photo Taken')"
$ReturnStr=SQLite("SQL",$_sql)

ShowStatus msg="off" Display=off

#Show input form to collect onwer ID's
WHILE TRUE
    $FormExit = form($Form,"")
    BEGINCASE 
				CASE $FormExit = "SystemExit"
					CANCEL

        CASE $FormExit = "OK_SameDay"
					# Calculate the caches which are found at the same day
					$_sql = "Select a.lParent from logs as a, logs as b where (a.lParent=b.lParent) and (a.lOwnerId=$FirstID) and (b.lOwnerId=$SecondID) and (a.lDate = b.lDate)"
					$ReturnStr=SQLite("SQL",$_sql)
        CASE $FormExit = "OK_No1No2"
					# Calculate the caches where cacher #1 found it before #2
					$_sql = "Select a.lParent from logs as a, logs as b where (a.lParent=b.lParent) and (a.lOwnerId=$FirstID) and (b.lOwnerId=$SecondID) and (a.lDate < b.lDate)"
					$ReturnStr=SQLite("SQL",$_sql)
        CASE $FormExit = "OK_No2No1"
					# Calculate the caches where cacher #2 found it before #1
					$_sql = "Select a.lParent from logs as a, logs as b where (a.lParent=b.lParent) and (a.lOwnerId=$FirstID) and (b.lOwnerId=$SecondID) and (a.lDate > b.lDate)"
					$ReturnStr=SQLite("SQL",$_sql)
        CASE $FormExit = "OK_Both"
					# Calculate the caches where both cachers found it
					$_sql = "Select a.lParent from logs as a, logs as b where (a.lParent=b.lParent) and (a.lOwnerId=$FirstID) and (b.lOwnerId=$SecondID)"
					$ReturnStr=SQLite("SQL",$_sql)
        CASE $FormExit = "Cancel"
					# Abort the macro
					CANCEL
    ENDCASE
		
		# Check if a filter should be set
		If Left($FormExit,3)="OK_"
			# Set speedmode of to show the result in the cache list
			SpeedMode Status=Off
			If IsEmpty($ReturnStr)
				# If no cache is in the result, show all as usual
				CancelFilter
				MsgOk msg="No caches"
			Else
				# Now set the macroflag for the found caches (very easy with the CodeMatch function
				$Return2 = CodeMatch($ReturnStr, $_NewLine, "M")
				# Activate the filter
				Mfilter expression=$d_MacroFlag
				# Show also the number of caches
				$ReturnStr=NumToStr($_filtercount)+" Caches"
				msgok msg=$ReturnStr	
			EndIf
			SpeedMode Status=On
		EndIf
ENDWHILE  


#Data variable to create the form elements:
 <Data> VarName=$form
#********************************************************************
# Form generated by GSAK form designer on So 07-Sep-2008 01:18:38
#********************************************************************

Name = Form1
  Type = Form
  Caption = Filter by find order
  Height = 337
  Width = 422

Name = OK_SameDay
  Type = Button
  Height = 25
  Left = 16
  Top = 200
  Width = 241
  Taborder = 8
  Caption = Filter for Same Day

Name = Cancel
  Type = Button
  Height = 25
  Left = 295
  Top = 216
  Width = 75
  Taborder = 9
  Caption = Done

Name = Label1
  Type = Label
  Height = 13
  Left = 18
  Top = 24
  Width = 246
  Caption = Enter the gc.com owner ID number of the person #1

Name = FirstID
  Type = Edit
  Height = 21
  Left = 18
  Top = 54
  Width = 121
  Taborder = 10

Name = Label2
  Type = Label
  Height = 13
  Left = 18
  Top = 96
  Width = 246
  Caption = Enter the gc.com onwer ID number of the person #2

Name = SecondID
  Type = Edit
  Height = 21
  Left = 18
  Top = 126
  Width = 121
  Taborder = 11

Name = OK_No1No2
  Type = Button
  Height = 25
  Left = 16
  Top = 234
  Width = 241
  Taborder = 12
  Caption = Filter for Person#1 found before Person#2

Name = OK_No2No1
  Type = Button
  Height = 25
  Left = 16
  Top = 268
  Width = 241
  Taborder = 13
  Caption = Filter for Person#2 found before Person#1

Name = OK_Both
  Type = Button
  Height = 25
  Left = 16
  Top = 166
  Width = 241
  Taborder = 14
  Caption = Filter Found by both

<enddata>

 
