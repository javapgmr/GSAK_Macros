#*******************************************
# MacVersion = 1.0
# MacDescription = Center on postcode via Geonames
# MacAuthor = Willum Kerker
# MacFileName = CenterOnPostcode.gsk
# MacUrl =
#*******************************************
# I know that there is already a function for this, but I 
# notich that the postcodes not always are correct in the 
# database. (I just hope they are better via Geonames :-))  
# 
# This macro will find postcodes via the Geonames database,
# so you must have an internet connection to use this macro. 
#
# In the "Enter (First part of) Postcode" part you can type a 
# postcode or the (beginning) part of a postcode. 
# 
# In the result part here will be a list of found postcodes 
# in the format Countrycode - Citty  (Postcode). 
# Select the one you need and press Center.
# 
# The maximum result amount is set to 10, you can change 
# this by changing the $maxRows variable. 
#*******************************************
Option Explicit=Yes

Declare Var=$form Type=String
Declare Var=$FormExit Type=String
Declare Var=$URLResults Type=String
Declare Var=$TotalResults Type=Numeric
Declare Var=$RegExResults Type=String 
Declare Var=$MaxRows Type=Numeric
Declare Var=$LatLng Type=String

$MaxRows=10

If $_count = 0 
  MsgOK msg="This macro cannot run in an empty database"
Else 
  $RegExResults = ""   
  While True
    $FormExit=Form("$form","")
    BeginCase
      Case $FormExit = "SystemExit"
        Cancel
      Case $FormExit = "ButtonExit"
        Cancel
      Case $FormExiT = "Search"
        GoSub Name=FillCombo
      Case $FormExit = "Center"
        GoSub Name=Center
    EndCase  
  EndWhile 
EndIf 


BeginSub name=FillCombo
  If IsEmpty($PostCode) 
    msgOK msg="No postcode entred"
  else 
    $URLResults = GetUrl("http://ws.geonames.org/postalCodeSearch?postalcode_startsWith=$PostCode&maxRows=$MaxRows")
    $TotalResults = Val(RegExSub("<totalResultsCount>(.*)</totalResultsCount>",$URLResults,1,1))     
    $RegExResults = ""
    while $totalresults <> 0 
      if IsEmpty(RegExSub("<countryCode>(.*)</countryCode>",$URLResults,$totalresults,1))
      else 
        $RegExResults = $RegExResults + RegExSub("<countryCode>(.*)</countryCode>",$URLResults,$totalresults,1) + " - "
        $RegExResults = $RegExResults + RegExSub("<name>(.*)</name>",$URLResults,$totalresults,1) 
        $RegExResults = $RegExResults + " (" + RegExSub("<postalcode>(.*)</postalcode>",$URLResults,$totalresults,1) + ")"
        $RegExResults = $RegExResults + "                                                                                                                                        <LatLng>"
        $RegExResults = $RegExResults + RegExSub("<lat>(.*)</lat>",$URLResults,$totalresults,1) + " " + RegExSub("<lng>(.*)</lng>",$URLResults,$totalresults,1) 
        $RegExResults = $RegExResults + "</LatLng>;"
      endif
      $totalresults = $totalresults - 1
    endwhile   
  endif
EndSub

BeginSub name=Center 
  If IsEmpty($Results) 
    msgOK msg="No results"
  else 
    $LatLng=RegExSub("<Latlng>(.*)</Latlng>",$Results,1,1)
    CENTER LOCATION="CenterOnPostcode" COORDINATES=$LatLng
    RETURN
  endif 
EndSub

<Data> VarName=$form
#********************************************************************
# Form generated by GSAK form designer on zo 27-jun-2010 11:39:38
#********************************************************************

Name = Form1
  Type = Form
  Caption = Center on Postcode
  Height = 179
  Width = 378

Name = PostCode
  Type = Edit
  Height = 21
  Left = 40
  Top = 32
  Width = 121
  Taborder = 8

Name = Center
  Type = Button
  Enabled = Yes
  Height = 25
  Left = 262
  Top = 71
  Width = 73
  Taborder = 9
  Caption = Center

Name = ButtonExit
  Type = Button
  Enabled = Yes
  Height = 25
  Left = 262
  Top = 104
  Width = 75
  Taborder = 10
  Caption = Exit

Name = Label1
  Type = Label
  Enabled = Yes
  Height = 13
  Left = 40
  Top = 16
  Width = 121
  Caption = Enter (First part of) Postcode

Name = Label2
  Type = Label
  Enabled = Yes
  Height = 13
  Left = 40
  Top = 56
  Width = 35
  Caption = Results

Name = Search
  Type = Button
  Height = 25
  Left = 174
  Top = 30
  Width = 75
  Taborder = 11

Name = Results
  Type = Combobox
  Height = 21
  Left = 40
  Top = 72
  Values = $RegExResults
  Width = 209
  Taborder = 12

<enddata>
