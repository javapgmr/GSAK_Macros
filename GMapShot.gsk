#*******************************************
# MacDescription = Add a google map url to the user notes
# MacFileName = GMapShot.gsk
# MacAuthor = Rhintl
# MacVersion=1.3
# MacUrl = http://gsak.net/board/index.php?showtopic=12993&view=getlastpost
#*******************************************
VERCHECK Version=8.1.0.000 (please update GSAK to the latest version to run this macro)
$RadiobuttonAddThis=true
$CheckboxHybrid=true
MACSETTINGS Type=R FileCheck=N
$mygmapkey=""
While True
  $FormExit = form($Form,"")
  # MsgOK msg=$FormExit
  BEGINCASE 
    CASE $FormExit = "SystemExit"
    CANCEL
  
    CASE $FormExit = "ButtonCancel"
    CANCEL
  
    CASE $FormExit = "ButtonGo"
    BEGINCASE
      CASE $RadiobuttonAddThis
        GoSub Name=ProcAddThis
      CASE $RadiobuttonRemoveThis
        GoSub Name=ProcRemoveThis
      CASE $RadiobuttonAddAll
        GoSub Name=ProcAddAll
      CASE $RadiobuttonRemoveAll
        GoSub Name=ProcRemoveAll
    ENDCASE
    MACSETTINGS Type=S Vars=RadiobuttonAddThis,RadiobuttonRemoveThis,RadiobuttonAddAll,RadiobuttonRemoveAll,CheckboxHybrid,CheckboxSatellite,CheckboxRoadmap,CheckboxTerrain

    CANCEL
        
  ENDCASE 
ENDWHILE

BeginSub Name=ProcAddThis
  Transaction action=Begin
  GoSub Name=AddMapImage
  ShowStatus msg="Gmap added to this waypoint."
  Transaction action=End
EndSub

BeginSub Name=ProcRemoveThis
  Transaction action=Begin
  GoSub Name=RemoveMapImage
  ShowStatus msg="Gmap removed from this waypoint."
  Transaction action=End
EndSub

BeginSub Name=ProcAddAll
  $recno = 0
  GoTo Position=top
  Transaction action=Begin
  While not($_Eol)
    $recno = $recno + 1
    IF frac($Recno/10) = 0
      ShowStatus msg="Processing record $recno of $_count"
    endIf
    GoSub Name=AddMapImage
    GoTo Position=Next
  EndWhile
  ShowStatus msg="Gmap added to all waypoints."
  Transaction action=End
  Goto Position=Top
EndSub

BeginSub Name=ProcRemoveAll
  $recno = 0
  GoTo Position=top
  Transaction action=Begin
  While not($_Eol)
    $recno = $recno + 1
    IF frac($Recno/10) = 0
      ShowStatus msg="Processing record $recno of $_count"
    endIf
    GoSub Name=RemoveMapImage
    GoTo Position=Next
  EndWhile
  ShowStatus msg="Gmap removed from all waypoints."
  Transaction action=End
  Goto Position=Top
EndSub

BeginSub Name=AddMapImage
  $img = "*Begin GMapShot*" + $_NewLine
  if $CheckboxHybrid=True
    $thismaptype="hybrid"
    GoSub Name=AddIMGdata
  endIf
  if $CheckboxRoadmap=True
    $thismaptype="roadmap"
    GoSub Name=AddIMGdata
  endIf
  if $CheckboxSatellite=True
    $thismaptype="satellite"
    GoSub Name=AddIMGdata
  endIf
  if $CheckboxTerrain=True
    $thismaptype="terrain"
    GoSub Name=AddIMGdata
  endIf
  $img = $img +  "*End GMapShot*"
  $UserNote = $d_UserNote
  # now if already a gmapshot in user notes it will be replaced
  if RegEx("\*Begin GMapShot\*",$UserNote)
    # preserve any notes before the gmapshot
    $Part1 = Extract($d_UserNote,"*Begin GMapShot*",1)
    # preserve any notes after the gmapshot
    $Part2 = Extract($d_UserNote,"*End GMapShot*",2)
    # now insert the new gmapshot
    $UserNote = $Part1 + $img + $Part2
  Else
    # else part - No GMapShot, but we must put them in before the User logs
    If RegEx("\$~",$UserNote)
      # User losgs found, so seperate the user notes from the logs
      $User = Extract($UserNote,"$~",1)
      $Log = Extract($UserNote,"$~",2)
      # now put in the gmapshot between the two
      $UserNote = $User + $_NewLine + $img + $_NewLine + "$~" + $Log
    else
      # else there are no user logs, so just add the GMapShot to any user notes
      $UserNote =  $UserNote + $_NewLine + $img
    EndIf
  EndIf
  $d_UserNote = $UserNote
EndSub

BeginSub Name=AddIMGdata
  # break up the mapurl so it is not just one big long line
  $img = $img + "<img src='"
  $img = $img + "http://maps.google.com/staticmap?center=%lat,%lon&zoom=MaxZoom()&size=300x300&format=jpg&maptype="
  $img = $img + $thismaptype
  $img = $img + "&mobile=true&markers=%lat,%lon,blue"+"'"
  $img = $img + " Alt='Google map'/>" + $_NewLine
EndSub  


BeginSub Name=RemoveMapImage
  $UserNote = $d_UserNote
  # now if already a gmapshot in user notes it will be replaced
  if RegEx("\*Begin GMapShot\*",$UserNote)
    # preserve any notes before the gmapshot
    $Part1 = Extract($d_UserNote,"*Begin GMapShot*",1)
    # preserve any notes after the gmapshot
    $Part2 = Extract($d_UserNote,"*End GMapShot*",2)
    # now insert then new gmapshot
    $UserNote = $Part1 + $Part2
  EndIf
  $d_UserNote = $UserNote
EndSub  

<Data> VarName=$form
#********************************************************************
# Form generated by GSAK form designer on So 04-Nov-2012 12:25:13
#********************************************************************

Name = Form1
  Type = Form
  Caption = GMapShot
  Height = 217
  Left = 531
  Top = 407
  Width = 454

Name = GroupboxAction
  Type = Groupbox
  Caption = Action:
  Height = 97
  Left = 8
  Top = 48
  Width = 273
  Taborder = 0

Name = GroupBoxMapType
  Type = Groupbox
  Caption = Map type(s) to add:
  Height = 97
  Left = 296
  Top = 48
  Width = 129
  Taborder = 4

Name = Label1
  Type = Label
  Height = 13
  Left = 8
  Top = 8
  Width = 228
  Caption = Adds the url of a google map to the user notes.

Name = Label2
  Type = Label
  Height = 13
  Left = 8
  Top = 24
  Width = 432
  Caption = Click database->grab images to make the map available during offline use and html export.

Name = RadiobuttonAddThis
  Type = Radiobutton
  Container = GroupboxAction
  Height = 17
  Left = 8
  Top = 16
  Width = 121
  Taborder = 0
  Caption = Add to this waypoint

Name = RadiobuttonRemoveThis
  Type = Radiobutton
  Container = GroupboxAction
  Height = 17
  Left = 8
  Top = 35
  Width = 185
  Taborder = 1
  Caption = Remove from this waypoint

Name = RadiobuttonAddAll
  Type = Radiobutton
  Container = GroupboxAction
  Height = 17
  Left = 8
  Top = 54
  Width = 201
  Taborder = 2
  Caption = Add to all waypoints in filter

Name = RadiobuttonRemoveAll
  Type = Radiobutton
  Container = GroupboxAction
  Height = 17
  Left = 8
  Top = 73
  Width = 241
  Taborder = 3
  Caption = Remove from all waypoints in filter

Name = ButtonGo
  Type = Button
  Enter = Yes
  Height = 25
  Left = 8
  Top = 152
  Width = 75
  Taborder = 1
  Caption = Go!

Name = ButtonCancel
  Type = Button
  Enter = No
  Escape = Yes
  Height = 25
  Left = 296
  Top = 152
  Width = 75
  Taborder = 2
  Caption = Cancel

Name = CheckboxHybrid
  Type = Checkbox
  Container = GroupBoxMapType
  Height = 17
  Left = 8
  Top = 16
  Width = 97
  Taborder = 0
  Caption = Hybrid

Name = CheckboxSatellite
  Type = Checkbox
  Container = GroupBoxMapType
  Height = 17
  Left = 8
  Top = 34
  Width = 97
  Taborder = 1
  Caption = Satellite

Name = CheckboxRoadmap
  Type = Checkbox
  Container = GroupBoxMapType
  Height = 17
  Left = 8
  Top = 52
  Width = 97
  Taborder = 2
  Caption = Roadmap

Name = CheckboxTerrain
  Type = Checkbox
  Container = GroupBoxMapType
  Height = 17
  Left = 8
  Top = 70
  Width = 97
  Taborder = 3
  Caption = Terrain

<enddata>


