#*******************************************
# MacDescription = Merge GPX Files
# MacFileName = MergeGPX.gsk
# MacAuthor = Kai Team
# MacVersion=1.21
# MacUrl=
#*******************************************

$FileNum=1
$GPXHead=""
$GPX=""
$SettingsFile=$_Install+"\macros\MergeGPX.dat"

IF FileExists($SettingsFile)
    $Settings = GetFile($SettingsFile)
    IF Left($Settings,7) = "*Error*"
        Pause Msg="$Settings"
        CANCEL
    ENDIF
    $SourceFolder=Extract($Settings,";",1)
    $DestFolder=Extract($Settings,";",2)
    $FileName=Extract($Settings,";",3)
ENDIF

WHILE TRUE
    $FormExit = form($Form,"")
    BEGINCASE 
        CASE $FormExit = "SystemExit"
            CANCEL Msg="Macro Cancelled"

        CASE $FormExit = "OK"
            GOSUB Name=SaveSettings
            $Directory=$_Quote + $SourceFolder + "\*.GPX" + $_Quote
            GOSUB Name=GetFileList
            FILEREAD File=C:\Temp\FileList.txt
            
            $Data = GetFile($SourceFolder + "\" + $line)
            IF Left($Data,7) = "*Error*"
                PAUSE Msg="$Data"
                CANCEL
            ENDIF
               
            IF $FileNum=1
                $Data=Replace("</gpx>","",$Data,TRUE)                
                $GPX=$Data + $_NewLine
                $Data=RegExData("<bounds*.*/>",$Data,1)
                $Expression=$_Quote + "-*\d*\.\d*" + $_Quote
                $MinLatStr1=RegExData($Expression, $Data, 1)
                $MinLat1=Val($MinLatStr1)
                $MinLonStr1=RegExData($Expression, $Data, 2)
                $MinLon1=Val($MinLonStr1)
                $MaxLatStr1=RegExData($Expression, $Data, 3)
                $MaxLat1=Val($MaxLatStr1)
                $MaxLonStr1=RegExData($Expression, $Data, 4)
                $MaxLon1=Val($MaxLonStr1)
            ELSE 
                $GpxHead = RegExData("(?s)<\?xml.*?/keywords>",$Data,1) + $_NewLine
                $Data=Replace($GpxHead,"",$Data,TRUE)
                $Data=Replace("</gpx>","",$Data,TRUE)
                GOSUB Name=CompareBounds
                $Data=RegExReplace("<bounds*.*/>",$Data,"")
                $GPX=$GPX + $Data + $_NewLine 
            ENDIF
            $FileNum=$FileNum+1
            ENDREAD
            
            $GPX=$GPX + "</gpx>"         
        
            IF At(",",RegExData("<bounds*.*/>",$GPX,1))>0
            $GPX=Replace(",", ".", RegExData("<bounds*.*/>",$GPX,1), TRUE)
            ENDIF
            
            $Data = PutFile($DestFolder + "\" + $FileName + ".gpx",$GPX)
            IF Left($Data,7) = "*Error*"
                PAUSE Msg="$Data"
                Cancel
            ENDIF
            BREAK

        CASE $FormExit = "Cancel"
            CANCEL Msg="Macro Cancelled"
    ENDCASE 
ENDWHILE    



BEGINSUB Name=GetFileList
    #Determine the Command Processor for the user's version of Windows
    $comspec = GetEnvV("comspec")

    #Run the Command Processor to obtain the directory listing of the specified path
    RUNPGM pgm=$comspec parms=/C DIR $Directory /a:-d /b  > C:\Temp\FileList.txt  Wait=Yes
    $FileList=GetFile("C:\Temp\FileList.txt")
ENDSUB

BEGINSUB Name=SaveSettings
    $Settings=$SourceFolder + ";" + $DestFolder + ";" + $FileName + ";"
    $PutFile=PutFile($SettingsFile,$Settings)
    IF Left($PutFile,7) = "*Error*"
        PAUSE Msg="$Data"
        CANCEL 
    ENDIF
ENDSUB
    
BEGINSUB Name=CompareBounds
    $Data=RegExData("<bounds*.*/>",$Data,1)
    $Expression=$_Quote + "-*\d*\.\d*" + $_Quote
    $MinLatStr=RegExData($Expression, $Data, 1)
    $MinLat=Val($MinLatStr)
    $MinLonStr=RegExData($Expression, $Data, 2)
    $MinLon=Val($MinLonStr)
    $MaxLatStr=RegExData($Expression, $Data, 3)
    $MaxLat=Val($MaxLatStr)
    $MaxLonStr=RegExData($Expression, $Data, 4)
    $MaxLon=Val($MaxLonStr)

    IF $MinLat<$MinLat1
        $ReplaceExpression="minlat=" + $_Quote + "-*\d*\.\d*" + $_Quote
        $GPX=RegExReplace($ReplaceExpression, $GPX, "minlat=" + $_Quote + "$MinLat" + $_Quote) 
    ENDIF
    
    IF $MinLon<$MinLon1
        $ReplaceExpression="minlon=" + $_Quote + "-*\d*\.\d*" + $_Quote
        $GPX=RegExReplace($ReplaceExpression, $GPX,"minlon=" + $_Quote + "$MinLon" + $_Quote) 
    ENDIF    
    
    IF $MaxLat>$MaxLat1
        $ReplaceExpression="maxlat=" + $_Quote + "-*\d*\.\d*" + $_Quote
        $GPX=RegExReplace($ReplaceExpression, $GPX, "maxlat=" + $_Quote + "$MaxLat" + $_Quote) 
    ENDIF
    
    IF $MaxLon>$MaxLon1
        $ReplaceExpression="maxlon=" + $_Quote + "-*\d*\.\d*" + $_Quote
        $GPX=RegExReplace($ReplaceExpression, $GPX, "maxlon=" + $_Quote + "$MaxLon" + $_Quote) 
    ENDIF    
ENDSUB



<Data> VarName=$form
#********************************************************************
# Form generated by GSAK form designer on Sat 05-Apr-2008 15:28:18
#********************************************************************

Name = Form1
  Type = Form
  Height = 280
  Width = 500
  Caption=Merge GSAK Genearted GPX Files

Name = Label1
  Type = Label
  Height = 18
  Left = 15
  Size = 11
  Top = 6
  Width = 461
  Caption = All GPX files in the speficied folder will be merged into a single GPX file

Name = Label2
  Type = Label
  Height = 13
  Left = 18
  Top = 44
  Width = 251
  Caption = Select the folder than contains the files to be merged:

Name = SourceFolder
  Type = Folder
  Height = 21
  Left = 18
  Top = 66
  Width = 457
  Taborder = 8

Name = Label3
  Type = Label
  Height = 13
  Left = 18
  Top = 104
  Width = 226
  Caption = Select the folder to save the combined GPX file:

Name = DestFolder
  Type = Folder
  Height = 21
  Left = 18
  Top = 126
  Width = 457
  Taborder = 9

Name = Label4
  Type = Label
  Height = 13
  Left = 18
  Top = 168
  Width = 172
  Caption = Type the name for the combined file:

Name = FileName
  Type = Edit
  Height = 21
  Left = 210
  Top = 162
  Width = 265
  Taborder = 10

Name = OK
  Type = Button
  Height = 25
  Left = 126
  Top = 204
  Width = 75
  Taborder = 11
  Caption = OK

Name = Cancel
  Type = Button
  Height = 25
  Left = 312
  Top = 204
  Width = 75
  Taborder = 12
  Caption = Cancel

<enddata>
