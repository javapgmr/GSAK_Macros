#*******************************************
# MacDescription = Remove Duplicate Waypoints from a Database
# MacFileName = RemoveDups.gsk
# MacAuthor = Kai Team
# MacVersion=5.0
#*******************************************
$OlderRB=TRUE
$Prompt=TRUE

$_sql="Select Code from caches where $_Where"
$FilterCodes=Sqlite("sql",$_sql)
$FilterCodes="'" + Replace($_CrLf,"','",$FilterCodes) + "'"

MACROFLAG Type=Clear Range=All
MACSETTINGS Type=R FileCheck=N

WHILE TRUE
    $FormExit = form($Form1,"")
    BEGINCASE
        CASE $FormExit = "SystemExit"
        RETURN Msg="Macro Canceled"

        CASE $FormExit = "OK"
            MACSETTINGS Type=S Vars=OlderRB,NewerRB,PrefixRB,Prefix,Prompt
        GOSUB Name=Run
        BREAK

        CASE $FormExit = "Cancel"
        RETURN Msg="Macro Canceled"
    ENDCASE
ENDWHILE

BEGINSUB Name=Run
    #Select cache codes with duplicate coordinates to be deleted, contingent on the option selected by the user
    BEGINCASE
        CASE $PrefixRB
            $_sql="Select Code FROM Caches WHERE Code IN($FilterCodes) AND Round(Latitude,6)||' '||Round(Longitude,6) IN (Select Round(Latitude,6)||' '||Round(Longitude,6) AS LatLon from Caches  WHERE $_Where GROUP BY LatLon HAVING Count(LatLon)>1) AND g_Regex('^$Prefix',Code)"

        CASE $OlderRB
            $_sql="select d1.code from caches d1, caches d2 where d1.Code IN($FilterCodes) AND Round(d1.latitude,6)||' '||Round(d1.longitude,6) in (Select Round(latitude,6)||' '||Round(longitude,6) FROM Caches group by (Round(Latitude,6)||' '||Round(Longitude,6)) having count(Round(Latitude,6)||' '||Round(Longitude,6)) > 1) and Round(d1.Latitude,6)||' '||Round(d1.Longitude,6)=Round(d2.Latitude,6)||' '||Round(d2.Longitude,6) and d1.rowid < d2.rowid"

        CASE $NewerRB
            $_sql="select d1.code from caches d1, caches d2 where d1.Code IN($FilterCodes) AND Round(d1.latitude,6)||' '||Round(d1.longitude,6) in (Select Round(latitude,6)||' '||Round(longitude,6) FROM Caches group by (Round(Latitude,6)||' '||Round(Longitude,6)) having count(Round(Latitude,6)||' '||Round(Longitude,6)) > 1) and Round(d1.Latitude,6)||' '||Round(d1.Longitude,6)=Round(d2.Latitude,6)||' '||Round(d2.Longitude,6) and d1.rowid > d2.rowid"
    ENDCASE

    $DupCodes=Sqlite("sql",$_sql)
    $DupCodes="'" + Replace($_CrLf,"','",$Dupcodes) + "'"

    #Set macro flags and filter for dup codes to be deleted
    $_sql="Update caches set Macroflag=1 WHERE Code in ($DupCodes)"
    $Status=Sqlite("sql",$_sql)

    MFILTER Where=MACROFLAG

    IF $_FilterCount>0 AND $Prompt
        WHILE TRUE
            $FormExit = Form($ConfirmDelete,"")
            BEGINCASE
                CASE $FormExit = "SystemExit"
                RETURN Msg="Macro Canceled"

                CASE $FormExit = "OK"
                GOSUB Name=DELETE
                BREAK

                CASE $FormExit = "Cancel"
                RETURN
            ENDCASE
        ENDWHILE
    ELSE
        GOSUB Name=DELETE
    ENDIF
ENDSUB

BEGINSUB Name=DELETE
IF $_FilterCount>0
    $_sql="DELETE FROM Caches WHERE $_Where"
    $Status=Sqlite("sql",$_sql)
ELSE
   $Message="There are no duplicate waypoints to delete."
   MSGOK Msg=$Message
ENDIF
ENDSUB

<data> VarName=$Delete
[TfmDelete]
cbxStop.Checked=False
rbtFilter.Checked=True
rbtFlagged.Checked=False
rbtOnly.Checked=False
<enddata>

<Data> VarName=$form1
#********************************************************************
# Form generated by GSAK form designer on Sat 17-Mar-2012 17:48:49
#********************************************************************

Name = Form1
  Type = Form
  Caption = Remove Waypoints With Duplicate Coordinates
  Height = 253
  Top = 348
  Width = 320

Name = OK
  Type = Button
  Height = 25
  Left = 54
  Top = 180
  Width = 75
  Taborder = 8
  Caption = OK

Name = Cancel
  Type = Button
  Height = 25
  Left = 183
  Top = 180
  Width = 75
  Taborder = 9
  Caption = Cancel

Name = OlderRB
  Type = Radiobutton
  Fontsize = 10
  Height = 20
  Left = 27
  Top = 42
  Width = 250
  Taborder = 12
  Caption = Remove the older ones (created first)

Name = Label1
  Type = Label
  Height = 17
  Left = 27
  Size = 10
  Top = 12
  Width = 254
  Caption = Select the duplicate waypoints to remove:

Name = NewerRB
  Type = Radiobutton
  Fontsize = 10
  Height = 20
  Left = 27
  Top = 64
  Width = 250
  Taborder = 13
  Caption = Remove the newer ones (created later)

Name = Prompt
  Type = Checkbox
  Color = -2147483633
  Fontcolor = 151
  Fontsize = 10
  Height = 20
  Left = 28
  Top = 138
  Width = 155
  Taborder = 14
  Caption = Prompt before deleting

Name = PrefixRB
  Type = Radiobutton
  Fontsize = 10
  Height = 20
  Left = 27
  Top = 92
  Width = 250
  Taborder = 15
  Caption = Remove duplicates with this prefix:

Name = Prefix
  Type = Edit
  Height = 21
  Left = 253
  Top = 96
  Width = 31
  Taborder = 16

<enddata>

<Data> VarName=$ConfirmDelete
#********************************************************************
# Form generated by GSAK form designer on Sat 17-Mar-2012 22:21:24
#********************************************************************

Name = Form2
  Type = Form
  Caption = Confirm Deletion!
  Height = 151
  Width = 372

Name = Label1
  Type = Label
  Color = 4194368
  Height = 24
  Left = 31
  Size = 14
  Top = 15
  Width = 302
  Caption = Delete or filter  the cache(s) shown:

Name = Cancel
  Type = Button
  Height = 25
  Left = 200
  Top = 63
  Width = 79
  Taborder = 2
  Caption = Filter Only

Name = OK
  Type = Button
  Height = 25
  Left = 85
  Top = 64
  Width = 79
  Taborder = 1
  Caption = DELETE

<enddata>



