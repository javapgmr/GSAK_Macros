#*******************************************
# MacDescription = Count all waypoints in all databases
# MacFileName = CountAllWaypoints.gsk
# MacAuthor = Kai Team
# MacVersion=1.05
# MacUrl=http://gsak.net/board/index.php?showtopic=6460&view=findpost&p=38461
#*******************************************

# Save Filter to restore later and then cancel filter
$SavedFilter = SaveFilter()
CANCELFILTER

# Gather List of User's Databases and Add a termination string
$DBs=SysInfo("Databases")
$DBs=$DBs + "; " + "DB~END;"

# Initialize Variables
$Out=""
$CurrentDB=""
$NextDB=""
$NumWP=0
$Count=1
$comspec = GetEnvV("comspec")
$File=$_Install + "\Macros\CountWaypoints.txt"

# Save Current Database to Restore Later
$StartDB=$_CurrentDatabase

#Loop through each database, counting the number of waypoints in each and the number of databases
WHILE $NextDB<>" DB~END"
    $CurrentDB=Extract($DBs, ";", $Count)
    DATABASE Name=$CurrentDB Action=Select
    $Out=$Out + " " + $CurrentDB + ": " + NumToStr($_Count) + $_NewLine
    $NumWP=$NumWP + $_Count
    $Count=$Count+1
    $NextDB=Extract($DBs, ";", $Count)
ENDWHILE

#Calculate the average number of waypoints per database
$Average=Int($NumWP/$Count)

#Output results to memo box on output form
$Memo1="Total Waypoints in GSAK Databases " + DateFormat($_Today) + ": " + NumToStr($NumWP) + $_NewLine
$Memo1=$Memo1 + "Databases in GSAK: " + NumToStr($Count) + $_NewLine 
$Memo1=$Memo1 + "Average Waypoints per Database: " + NumToStr($Average) + $_Newline + $_Newline 
$Memo1= $Memo1 + "Waypoints in each database: " + $_Newline + $Out

#Restore Starting Database
IF $StartDB<>$_CurrentDatabase
    DATABASE Name=$StartDB Action=Select
ENDIF

#Restore starting filter
$Restore=RestoreFilter($SavedFilter,TRUE)

#Show results form
IF Showform($form)
    IF $Button1
        CLIP Data=$Memo1
        CANCEL
    ENDIF 
    IF $Button2
        $SavedFile=PUTFILE($file, $Memo1)
        IF Left($File, 7) = "*Error*"
            CANCEL Msg="Unable to write to $file!"
        ENDIF
        RUNPGM pgm=notepad parms=/p "$File"
    ENDIF 
ENDIF

<Data> VarName=$form
#********************************************************************
# Form generated by GSAK form designer on Mon 22-Oct-2007 05:46:37
#********************************************************************

Name = Form1
  Type = Form
  Caption = Waypoints in GSAK Databases:
  Color = 11645283
  Height = 661
  Width = 500

Name = Memo1
  Type = Memo
  Color = 15724527
  Height = 517
  Left = 35
  Readonly = Yes
  Scrollbars = Both
  Size = 12
  Style = bold
  Top = 24
  Width = 422
  Wrap = No

Name = Button1
  Type = Button
  Height = 25
  Left = 111
  Top = 570
  Width = 75
  Caption = OK

Name = Button2
  Type = Button
  Height = 25
  Left = 305
  Top = 570
  Width = 75
  Caption = Print

<enddata>


