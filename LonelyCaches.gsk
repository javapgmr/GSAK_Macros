#*******************************************
# MacVersion = 0.2
# MacDescription = Filter to only show caches where the last found log before my found log is at least nn days earlier than mine.
# MacAuthor = Barry Davies
# MacFileName = LonelyCaches.gsk
# MacUrl = http://gsak.net/board/index.php?showtopic=6337
#*******************************************

VerCheck Version=7.1.0.0 (please update GSAK to the latest version to run this macro - see http://gsak.net)

If Sysinfo("gsakini;TfmConfig;rbtId.Checked") = "True"
  $userid = Sysinfo("gsakini;TfmConfig;EdtGeoName.Text") 
EndIf
$days = NumToStr( 90 )

$foundbymedate = [00000000]
$count = 0
$lonelycount = 0

$foundprevtomedate = [00000000]
$foundprevtomedays = 0

If ShowForm($form)
  If $ok
    $me = Val( $userid )
    $mindaysbetween = Val( $days )
  EndIf
  If $cancel
    cancel
  EndIf
Else
  cancel
EndIf

Goto Position=Top
macroflag type=clear range=all

While not($_EOL)

    $count = $count + 1
    ShowStatus Msg="Processing  $count of $_Count"

    Table Active=logs Scope=parent

    $foundbymedate = 0
    $foundprevtomedate = 0
    $foundprevtomedays = 0

    While not($_eol) AND ($foundprevtomedate = [00000000])

       If $d_lOwnerid <> $me And $foundbymedate <> [00000000] and $foundprevtomedate = [00000000]
          if $d_lType = "Found it" or $d_lType = "Attended" or $d_lType = "Webcam Photo Taken"
            $foundprevtomedate = $d_lDate
            $foundprevtomedays = $foundbymedate - $foundprevtomedate
            if $foundprevtomedays >= $mindaysbetween 
              macroflag type=set range=1
              $lonelycount = $lonelycount + 1
           endif
         endif
       EndIf


       If $d_lOwnerid = $me
          if $d_lType = "Found it" or $d_lType = "Attended" or $d_lType = "Webcam Photo Taken"
            $foundbymedate = $d_lDate
          endif
      EndIf

      Goto Position=Next

    EndWhile

    Table Active=caches

    Goto Position=Next

EndWhile

ShowStatus Msg="Macro ended." Display=Off

If $lonelycount > 0
  msgok msg=" Macro found $lonelycount caches that match the criteria. $_Newline It will now filter the list to show only these caches."
  mfilter expression= $d_Macroflag = true
Else
  msgok msg=" No caches found that match the criteria. $_Newline The current list of caches will not be changed. "
EndIf


<Data> Varname=$form
#********************************************************************
# Form generated by GSAK form designer on Mon 01-Oct-2007 14:15:23
#********************************************************************

Name = Form1
  type = form
  height = 200
  width = 310
  caption = Lonely Caches

Name = LblUserID
  type = Label
  left = 32
  top = 23
  caption = My GC User ID is

Name = LblDays1
  type = Label
  left = 33
  top = 55
  caption = Filter current list to only show caches that I have found

Name = LblDays2
  type = Label
  left = 33
  top = 70
  caption = and where the gap before my found log to the previous

Name = LblDays3
  type = Label
  left = 33
  top = 86
  caption = found log is at least

Name = LblDays4
  type = Label
  left = 185
  top = 86
  caption = days

Name = userid
  type = Edit
  left = 130
  top = 19
  height = 21
  width = 121

Name = days
  type = Edit
  left = 130
  top = 86
  height = 21
  width = 50

Name = ok
  type = Button
  left = 43
  top = 134
  height = 25
  width = 75
  caption = OK
  enter = yes

Name = cancel
  type = Button
  left = 180
  top = 134
  height = 25
  width = 75
  caption = Cancel
  escape = yes

<EndData>

