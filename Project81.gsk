#*******************************************
# MacVersion = 2.3
# MacDescription = set a filter for all caches you need for your project 81
# MacAuthor = Krolock
# MacFileName = Project81.gsk
# MacUrl =
#*******************************************
#SHOWSTATUS msg="Macro Starting" Width=350
# prepare and run DBSelector form

$run = false

GoSub name="initialize"

While True # Infinite loop to redisplay form as required
  $FormExit = form($form,"")
  BeginCase
    Case $FormExit = "DelayExit"
      break
    Case $FormExit = "SystemExit"
      break
    Case $FormExit = "btnCancel"
      break
    Case $FormExit = "btnOk"
	$validate = true
      	if $cbDifStart > $cbDifEnd
		MsgOK msg="The start value of difficulty must be smaller than the end value"
		$validate = false
	endif
	if $cbTerStart > $cbTerEnd
		MsgOK msg="The start value of terrain must be smaller than the end value"
		$validate = false
	endif
	if $validate
		$run = true
	      	break
	endIf
  EndCase
EndWhile
if not ($run)
	return
ENDIF

GoSub name="evaluateFormValues"

# clear all filter and set databases and start values
MacroFlag Type=Clear Range=All

DATABASE Name="$cacheDB" action="select"

$path = RegExData(".*(?=.*\\)",$_CurrentDataPath,1)
$path = $path + "\$foundDB\Sqlite.db3"
$sqlInputFile = SqlQuote($path)

$_sql ="ATTACH $sqlInputFile AS myFoundDB"
$status = Sqlite("sql",$_sql)

$_sql="CREATE TEMP TABLE codesToFound AS SELECT c.Code FROM caches c LEFT OUTER JOIN ( SELECT Difficulty, Terrain FROM ( SELECT Difficulty, Terrain, COUNT(*) i FROM myFoundDB.Caches where Found GROUP BY Difficulty, Terrain ) WHERE i > $nrOfFounds ) f ON c.Difficulty = f.Difficulty AND c.Terrain = f.Terrain WHERE (f.Difficulty IS NULL OR f.Terrain IS NULL) AND c.Difficulty >= 	$difStart AND c.Difficulty <= $difEnd AND c.Terrain >= $terStart AND c.Terrain <= $terEnd Order by c.Difficulty, c.Terrain"
$status = Sqlite("sql",$_sql)

mfilter where=Code IN (SELECT Code from codesToFound)

$_sql = "DETACH myFoundDB"
$status = Sqlite("sql",$_sql)

if $_FilterCount = 0
	MsgOK msg="Their are no caches that can help you for your project 81"
else
	$message = NumToStr($_FilterCount) + " caches found that are suitable for your project 81"
	MsgOK msg=$message
EndIf

BeginSub name="initialize"
	$databases = sysinfo("databases")

	$oneToFive = "1;1.5;2;2.5;3;3.5;4;4.5;5"
	$settingsFile = $_Install + "\Macros\Project81.xml"
	if FileExists($settingsFile)
		MacSettings Type=R File=$settingsFile FileCheck=N
	else
		$cbDifStart = "1"
		$cbTerStart = "1"
		$cbDifEnd = "5"
		$cbTerEnd = "5"
		$cbFoundDB = "default"
		$cbCacheDB = "default"
		$cbNrOfFounds = "0"
	endif
EndSub

BeginSub name="evaluateFormValues"
	$cacheDB = $cbCacheDB
	$foundDB = $cbFoundDB

	$difStart = Val($cbDifStart)
	$difEnd = Val($cbDifEnd)
	$terStart = Val($cbTerStart)
	$terEnd = Val($cbTerEnd)
	$nrOfFounds = Val($cbNrOfFounds)
	MacSettings Type=S Vars=cbDifStart,cbDifEnd,cbTerStart,cbTerEnd,cbFoundDB,cbCacheDB,cbNrOfFounds File=$settingsFile FileCheck=N
EndSub

<Data> VarName=$form
#********************************************************************
# Form generated by GSAK form designer on Fr 04-Mai-2012 16:38:40
#********************************************************************

Name = Form1
  Type = Form
  Height = 388
  Width = 427

Name = btnOk
  Type = Button
  Height = 25
  Left = 64
  Top = 312
  Width = 75
  Taborder = 8
  Caption = OK

Name = btnCancel
  Type = Button
  Height = 25
  Left = 192
  Top = 312
  Width = 75
  Taborder = 9
  Caption = Cancel

Name = Label1
  Type = Label
  Height = 13
  Left = 32
  Top = 24
  Width = 86
  Caption = Cache database

Name = Label2
  Type = Label
  Height = 13
  Left = 32
  Top = 56
  Width = 90
  Caption = Founds database

Name = cbCacheDB
  Type = Combobox
  Height = 21
  Left = 136
  Top = 24
  Values = $databases
  Width = 145
  Taborder = 10

Name = cbFoundDB
  Type = Combobox
  Height = 21
  Left = 136
  Top = 56
  Values = $databases
  Width = 145
  Taborder = 11

Name = cbDifStart
  Type = Combobox
  Height = 21
  Left = 136
  Top = 104
  Values = $oneToFive
  Width = 57
  Taborder = 12

Name = cbDifEnd
  Type = Combobox
  Height = 21
  Left = 136
  Top = 136
  Values = $oneToFive
  Width = 57
  Taborder = 13

Name = cbTerStart
  Type = Combobox
  Height = 21
  Left = 136
  Top = 184
  Values = $oneToFive
  Width = 57
  Taborder = 14

Name = cbTerEnd
  Type = Combobox
  Height = 21
  Left = 136
  Top = 216
  Values = $oneToFive
  Width = 57
  Taborder = 15

Name = Label3
  Type = Label
  Height = 13
  Left = 32
  Top = 104
  Width = 71
  Caption = Difficulty start

Name = Label4
  Type = Label
  Height = 13
  Left = 32
  Top = 136
  Width = 69
  Caption = Difficulty end

Name = Label5
  Type = Label
  Height = 13
  Left = 32
  Top = 184
  Width = 64
  Caption = Terrain start

Name = Label6
  Type = Label
  Height = 13
  Left = 32
  Top = 216
  Width = 62
  Caption = Terrain end

Name = Label7
  Type = Label
  Height = 13
  Left = 32
  Top = 264
  Width = 87
  Caption = Max nr of founds

Name = cbNrOfFounds
  Type = Combobox
  Height = 21
  Left = 136
  Top = 264
  Values = 0;1;2;3;4;5;6;7;8;9;10
  Width = 57
  Taborder = 18

<enddata>

