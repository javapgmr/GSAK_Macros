#*******************************************
# MacVersion = 1.3
# MacDescription = Live tracking, by polling center point from GPSr
# MacAuthor = Clyde
# MacFileName = LiveTrack.gsk
# MacUrl = http://gsak.net/board/index.php?showtopic=6345&view=findpost&p=37568
#*******************************************

# get the saved settings if file exists 
If FileExists($_Install + "\Macros\CenterPoll.dat")
  $data = GetFile($_Install + "\Macros\CenterPoll.dat")
  $ctype = Extract($data,";",1)
  If $ctype = "N"
    $rbtNmea = true
  EndIf
  If $ctype = "U"
    $rbtGarminUsb = true
  EndIf
  If $ctype = "S"
    $rbtGarminSerial = true
  Endif
  $edtComPort = Extract($data,";",2)
  $edtSeconds = Extract($data,";",3)
  # Just in case file permission error or something test for error
  IF Left($data,7) = "*Error*"
    Pause Msg="$File"
    Cancel
  EndIf 
Else
  $rbtNmea = true
  $edtSeconds = "45"
EndIf

While ShowForm($form)
  If $btnCancel 
    Break
  Endif

  If $btnOk
    If $rbtNmea
       $ctype = "N"
    EndIf
    If $rbtGarminUsb
       $ctype = "U"
    EndIf
    If $rbtGarminSerial
       $ctype = "S"
    EndIf

    $gpsfile = quote($_Install + "\temp\exgps.txt")
    $babel = quote($_Exepath + "\gpsbabel.exe")
    
    
    $status = PutFile($_Install + "\macros\CenterPoll.dat",$ctype + ";" + $edtComPort + ";" + $edtSeconds)
    speedmode status=off

    If $rbtNmea
      $command = $babel + " -i nmea,get_posn -f " + $edtComPort + " -o csv -F " + $gpsfile
    Endif
 
    If $rbtGarminUsb
       $command = $babel + " -i garmin,get_posn -f usb: -o csv -F " + $gpsfile
    Endif

    If $rbtGarminSerial
       $command = $babel + " -i garmin,get_posn -f " + $edtComPort + " -o csv -F " + $gpsfile
    Endif

    $status = PutFile($_Install + "\babel.bat",$command + $_NewLine)


    While true

        If FileExists($gpsfile)
         FileErase File=$gpsfile
       EndIf
 
       RunPgm Pgm="babel.bat" wait=yes
       $center = GetFile(replace($_quote,"",$gpsfile,true))
       Center Location="Center from Gpsr " Coordinates=$center 

      # Loop through each second of the delay so macro is responsive to the stop button at second precision
      $dsecs = val($edtSeconds)
      While $dsecs > 0
        Showstatus msg="Next poll in $dsecs seconds"
        Delay ms=1000
        $dsecs = $dsecs - 1
      EndWhile

    EndWhile
  
  EndIf
EndWhile 



<Data> VarName=$form
#********************************************************************
# Form generated by GSAK form designer on Sun 30-Sep-2007 14:19:35
#********************************************************************

Name = Form1
  Type = Form
  Height = 264
  Width = 362

Name = grpType
  Type = Groupbox
  Caption = Conection type
  Height = 57
  Left = 24
  Top = 40
  Width = 297

Name = Label1
  Type = Label
  Color = 255
  Height = 16
  Left = 64
  Size = 10
  Style = bold
  Top = 8
  Width = 183
  Caption = Poll center point from Gpsr

Name = lblComPort
  Type = Label
  Height = 13
  Left = 8
  Top = 118
  Width = 138
  Caption = Com port (Com1 or Com2 etc)

Name = edtComPort
  Type = Edit
  Height = 21
  Left = 157
  Top = 112
  Width = 73

Name = lblSeconds
  Type = Label
  Height = 13
  Left = 10
  Top = 149
  Width = 132
  Caption = Seconds between each poll

Name = edtSeconds
  Type = Edit
  Height = 21
  Left = 159
  Top = 146
  Width = 73

Name = btnOk
  Type = Button
  Height = 25
  Left = 86
  Top = 189
  Width = 75
  Caption = OK

Name = btnCancel
  Type = Button
  Height = 25
  Left = 174
  Top = 189
  Width = 75
  Caption = Cancel

Name = rbtNmea
  Type = Radiobutton
  Container = grpType
  Height = 17
  Left = 16
  Top = 24
  Width = 15

Name = rbtGarminUsb
  Type = Radiobutton
  Container = grpType
  Height = 17
  Left = 104
  Top = 24
  Width = 15

Name = rbtGarminSerial
  Type = Radiobutton
  Container = grpType
  Height = 17
  Left = 200
  Top = 24
  Width = 15

Name = Label2
  Type = Label
  Container = grpType
  Height = 13
  Left = 48
  Top = 24
  Width = 31
  Caption = NMEA

Name = Label3
  Type = Label
  Container = grpType
  Height = 13
  Left = 128
  Top = 24
  Width = 58
  Caption = Garmin USB

Name = Label4
  Type = Label
  Container = grpType
  Height = 13
  Left = 224
  Top = 24
  Width = 60
  Caption = Garmin serial

Name = Label5
  Type = Label
  Height = 13
  Left = 238
  Top = 118
  Width = 99
  Caption = Leave blank for USB

<enddata>



