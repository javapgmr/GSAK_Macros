#*******************************************
# MacDescription =Add current cache to book mark list
# MacFileName =BookMarkIt.gsk
# MacAuthor = Kai Team
# MacVersion=1.1
#*******************************************
$SettingsFile=SlashAdd($_AppData) + "Macros\BookMarkIt.xml"
$GUID=""
$BMName=""

MACSETTINGS Type=R FileCheck=N

IF $_ShiftKey OR Not(FileExists($SettingsFile))
    #Get Bookmark List
    $Action=GcApi("GetBookmarkListsForUser")
    IF $_GcApiError
        MSGOK msg=$Action
    ELSE
        $_sql="Select Data from gcapi WHERE Key Like('%/ListName')"
        $ListNames=Sqlite("sql",$_sql)
        $ListNames=Replace($_CrLf,";",$ListNames)
    ENDIF

    $Form1 = editform($Form1,"Combobox1","values",$ListNames)

    WHILE TRUE
        $FormExit = form($Form1,"")
        BEGINCASE
            CASE $FormExit = "SystemExit"
            RETURN Msg="Macro Canceled"

            CASE $FormExit = "OK"
            $_sql="SELECT data FROM gcapi WHERE key LIKE '%/ListGUID' AND rowid < "
            $_sql=$_sql + "(SELECT rowid FROM gcapi WHERE key LIKE '%/ListName' AND data='$Combobox1') ORDER BY rowid DESC LIMIT 1"
            $GUID=Sqlite("sql",$_sql)
            MACSETTINGS Type=S Vars=Combobox1,GUID
            GOSUB Name=Run
            BREAK

            CASE $FormExit = "Cancel"
            RETURN Msg="Macro Canceled"
        ENDCASE
    ENDWHILE
ELSE
    GOSUB Name=Run
ENDIF

BEGINSUB Name=Run
    #Add to bookmark list
    $AddtoList=RegExReplace("~~GUIID~~",$AddToList,$GUID)
    $AddToList=RegExReplace("~~Code~~",$AddToList,$d_Code)
    $Action=GcApi("AddGeocachesToBookmarkList",$AddToList)
    IF $_GcApiError
        MSGOK msg=$Action
    ENDIF
ENDSUB

<Data> VarName=$AddToList
    <AddGeocachesToBookmarkListRequest xmlns="http://www.geocaching.com/Geocaching.Live/data">
      <AccessToken>{ACCESSTOKEN}</AccessToken>
      <BookmarkListGuid>~~GUIID~~</BookmarkListGuid>
      <CacheCodes>
        <string xmlns="http://schemas.microsoft.com/2003/10/Serialization/Arrays">~~Code~~</string>
      </CacheCodes>
    </AddGeocachesToBookmarkListRequest>
<enddata>

<Data> VarName=$form1
#********************************************************************
# Form generated by GSAK form designer on Sun 24-Feb-2013 20:46:55
#********************************************************************

Name = Form1
  Type = Form
  Caption = Book Mark It
  Height = 139
  Width = 500

Name = OK
  Type = Button
  Height = 25
  Left = 123
  Top = 57
  Width = 75
  Taborder = 8
  Caption = Book Mark It

Name = Cancel
  Type = Button
  Height = 25
  Left = 294
  Top = 57
  Width = 75
  Taborder = 9
  Caption = Cancel

Name = Label1
  Type = Label
  Height = 16
  Left = 32
  Size = 10
  Top = 14
  Width = 158
  Caption = Select the Bookmark List:

Name = Combobox1
  Type = Combobox
  Height = 21
  Left = 195
  Top = 12
  Width = 265
  Taborder = 12

<enddata>
