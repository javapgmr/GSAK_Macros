################################################################################ 
#                             Vigenère Cipher Tool                             #
#                               by DocDiTTo 2010                               # 
#                                                                              # 
#                                                                              #
# MacDescription = Vigenere Cipher Implementation                              #
# MacFileName = Vigenere_Cipher.gsk    																				 #
# MacAuthor = DocDiTTo                                                         #
# MacVersion=1.0																															 #
# MacUrl=http://gsak.net/board/index.php?showtopic=00000											 #
################################################################################


# Uncomment the 2 lines below to use the same example as the Wikipedia writeup
# on the Vigenere cipher.
#$memoInput = "ATTACKATDAWN"
#$memoKey = "LEMON"

$Alphabet = "ABCDEFGHIJKLMNOPQRSTUVWXYZ"
$OutputText = ""

WHILE True 
  $FormExit = form($form,"")
  BEGINCASE
    Case $FormExit = "DelayExit"
      Return
    Case $FormExit = "SystemExit"
      return
    Case $FormExit = "btnEncrypt"
   		$Decode = False
   		GOSUB Name=DoVigenereCipher  	
    Case $FormExit = "btnDecrypt"
   		$Decode = True
   		GOSUB Name=DoVigenereCipher
  ENDCASE
ENDWHILE

BEGINSUB Name=DoVigenereCipher
	    $memoInput = keep(Upper($memoInput),"[A-Z]*","R") # Only allow chars A-Z
    	$InputText = $memoInput
    	$memoKey = keep(Upper($memoKey),"[A-Z]*","R") # Only allow chars A-Z
    	$Key = $memoKey
    	$OutputText = ""
			WHILE len($Key) < len($InputText)
				$Key = $Key + $Key
			ENDWHILE
			$LoopCount = 0
			WHILE $LoopCount < len($InputText)
				$LoopCount = $LoopCount + 1
				$ShiftNum = ASC(substr($Key,$LoopCount,1))-65
 	      $Row = Right($Alphabet,Len($Alphabet)-$Shiftnum)+left($Alphabet,$ShiftNum)
				$OneChar = substr($InputText,$LoopCount,1)
				IF $Decode = True
						# Decode the character
						$OutputText = $OutputText + substr($Alphabet,AT($OneChar,$Row),1)
				ELSE
					# Encode the character
				  $OutputText = $OutputText + substr($Row,AT($OneChar,$Alphabet),1)
				ENDIF
			ENDWHILE
      $MemoOutput = $OutputText 
ENDSUB
 
<Data> VarName=$form
#********************************************************************
# Form generated by GSAK form designer on Wed 03-Mar-2010 23:07:42
#********************************************************************

Name = Form1
  Type = Form
  Caption = Vigenère Cipher
  Height = 496
  Width = 514

Name = MemoInput
  Type = Memo
  Height = 113
  Left = 8
  Scrollbars = Both
  Top = 24
  Width = 481
  Wrap = Yes
  Taborder = 8

Name = Label1
  Type = Label
  Height = 13
  Left = 8
  Top = 8
  Width = 135
  Caption = Input (Ciphertext or Plaintext)

Name = MemoKey
  Type = Memo
  Height = 113
  Left = 8
  Scrollbars = Both
  Top = 165
  Width = 481
  Wrap = Yes
  Taborder = 9

Name = Label2
  Type = Label
  Height = 13
  Left = 8
  Top = 288
  Width = 32
  Caption = Output

Name = btnEncrypt
  Type = Button
  Height = 25
  Left = 120
  Top = 424
  Width = 113
  Taborder = 10
  Caption = Encrypt

Name = MemoOutput
  Type = Memo
  Height = 113
  Left = 8
  Readonly = Yes
  Scrollbars = Both
  Top = 304
  Width = 481
  Wrap = Yes
  Taborder = 11

Name = Label3
  Type = Label
  Height = 13
  Left = 8
  Top = 149
  Width = 71
  Caption = Encryption Key

Name = btnDecrypt
  Type = Button
  Height = 25
  Left = 248
  Top = 424
  Width = 113
  Taborder = 12
  Caption = Decrypt

<enddata>

