#*******************************************
# MacVersion = 1.0
# MacDescription = Caching in a group
# MacAuthor = clyde
# MacFileName = GroupCaching.gsk
# MacUrl = 
#*******************************************

VerCheck Version=7.2.1.15

GoSub Name=Declare
GoSub Name=GetSettings

While true
  $exit = Form($form,"")
  BeginCase
    case $btnExport
      GoSub Name=Export
      break
    case $btnImport
      Gosub Name=Import
      break
    case $exit = "SystemExit" or $btnCancel
      Cancel
  EndCase
EndWhile

#*******************************************************
#   Variable declarations
#*******************************************************

Beginsub Name=Declare
  Option Explicit=Yes

  Declare Var=$crlf Type=String
  Declare Var=$exit Type=String
  Declare Var=$form Type=String
  Declare Var=$recno Type=Numeric
  Declare Var=$status Type=String
  Declare Var=$work Type=String
  Declare Var=$FileName Type=String
  Declare Var=$data Type=String
  Declare Var=$yesno type=string
  Declare Var=$count type=numeric 
  Declare Var=$ExportFile Type=String
  Declare Var=$ImportFolder Type=String
  Declare Var=$Out Type=String
  $crlf = $_NewLine
  $out = ""
  $recno = 0
  $filename = $_Install + "\macros\GroupCaching.dat"
EndSub


#********************************************************************
# Get saved settings
#********************************************************************
BeginSub Name=GetSettings
  If FileExists($filename)
    $data = GetFile($filename)
    $ExportFile = extract($data,chr(255),1)
    $ImportFolder = extract($data,chr(255),2)
  endif
EndSub


#********************************************************************
# Import lists of found caches and match them
#********************************************************************
BeginSub name=Import

  # first up clear out all macro flags
  MacroFlag type=clear range=all

  # save settings
  $status = Putfile($filename,$exportfile + chr(255) + $importfolder)
  $data = "dir /b " + quote($importFolder) + "\*.txt > " + quote("$_install\temp\files.txt")
  $status = PutFile("$_Install\babel.bat",$data)
  RunPgm Pgm="$_install\babel.bat" wait=yes
  $data = Getfile("$_install\temp\files.txt") 

  FileRead File="$_install\temp\files.txt"

    $data = GetFile($importFolder + "\" + $line)
    if at("GroupCachingIdString$crlf",$data) > 0
      
      $data = extract($data,"GroupCachingIdString$crlf",2)
      $status = CodeMatch($data,$crlf,"M")
    endif  

  EndRead

  Mfilter expression=$d_macroflag
  If $_FilterCount = 0
    Cancel msg="Sorry, the import did not match any caches to excude from this database"
  EndIf

  Mfilter expression= not($d_macroflag)
  If $_FilterCount = 0
    Cancel msg="Sorry, this database does no contain any unfound caches for the group"
  EndIf

  speedmode status=off
  MsgOk Msg="Filter now contains all caches not found by any member of the group"

EndSub

#********************************************************************
# Export list of found caches
#********************************************************************
Beginsub Name=Export

if FileExists($exportfile)
  $yesno = editform($yesno,"label1","caption",$exportfile)
  $exit = form($yesno,"")
  if $btnNo or $exit = "SystemExit"
    ExitSub
  endif
EndIF

  # save settings
  $status = Putfile($filename,$exportfile + chr(255) + $importfolder)

  # Generate export file
  $data ="GroupCachingIdString$crlf"
  $recno = 0
  Goto Position=Top
  While not($_eol)
    $recno = $recno + 1
    IF frac($Recno/10) = 0
      ShowStatus msg="Processing record $recno 0f $_count"
    endIf 
    if $d_found
      $count = $count + 1
      $data = $data + $d_code + $crlf
    endif
    Goto Position=Next
  EndWhile
  GoTo Position=Top
  $status = Putfile($exportfile,$data) 
  Showstatus msg=x display=off
  msgok msg="Export finished ($count finds)"
EndSub


<Data> VarName=$form
#********************************************************************
# Form generated by GSAK form designer on Sun 27-Jan-2008 22:15:55
#********************************************************************

Name = Form1
  Type = Form
  Caption = Group Caching
  Height = 266
  Width = 453

Name = Groupbox1
  Type = Groupbox
  Caption = Export finds
  Height = 59
  Left = 9
  Top = 48
  Width = 418
  Taborder = 0

Name = Groupbox2
  Type = Groupbox
  Caption = Import find files (select folder of exported finds)
  Height = 55
  Left = 9
  Top = 123
  Width = 418
  Taborder = 1

Name = Label1
  Type = Label
  Color = 255
  Height = 25
  Left = 103
  Size = 15
  Style = bold
  Top = 12
  Width = 182
  Caption = Caching in a group

Name = ExportFile
  Type = File
  Container = Groupbox1
  Height = 21
  Left = 10
  Top = 25
  Width = 305
  Taborder = 0

Name = btnExport
  Type = Button
  Container = Groupbox1
  Height = 25
  Left = 334
  Top = 23
  Width = 75
  Taborder = 1
  Caption = Export

Name = ImportFolder
  Type = Folder
  Container = Groupbox2
  Height = 21
  Left = 10
  Top = 23
  Width = 305
  Taborder = 0

Name = btnImport
  Type = Button
  Container = Groupbox2
  Height = 25
  Left = 334
  Top = 20
  Width = 75
  Taborder = 1
  Caption = Import

Name = btnCancel
  Type = Button
  Height = 25
  Left = 166
  Top = 193
  Width = 75
  Taborder = 10
  Caption = Cancel

<enddata>

<Data> VarName=$yesno
#********************************************************************
# Form generated by GSAK form designer on Sun 27-Jan-2008 19:25:52
#********************************************************************

Name = YesNo
  Type = Form
  Caption = Overwrite?
  Height = 166
  Width = 350

Name = Label1
  Type = Label
  Height = 13
  Left = 33
  Top = 29
  Width = 32

Name = Label2
  Type = Label
  Height = 13
  Left = 70
  Style = bold
  Top = 56
  Width = 202
  Caption = File name already exists, overwrite?

Name = btnYes
  Type = Button
  Height = 25
  Left = 68
  Top = 83
  Width = 75
  Taborder = 8
  Caption = Yes

Name = btnNo
  Type = Button
  Height = 25
  Left = 192
  Top = 82
  Width = 75
  Taborder = 9
  Caption = No

<enddata>

