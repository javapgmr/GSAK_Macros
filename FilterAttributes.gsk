#*******************************************
# MacVersion = 1.0
# MacDescription = Filter Attributes
# MacAuthor = Lignumaqua
# MacFileName = FilterAttributes.gsk
# MacUrl =
#*******************************************


$AttributeList = "Abandoned mines;Access or parking fee;Available at all times;Available during winter;Bicycles;Boat;Campfires;Camping available;Cliff / falling rocks;Climbing gear;Dangerous area;Difficult climbing;Dogs;Drinking water nearby;Flashlight required;Horses;Hunting;May require swimming;May require wading;Motorcycles;Needs maintenance;Off-road vehicles;Parking available;Picnic tables nearby;Poison plants;Public restrooms nearby;Public transportation;Quads;Recommended at night;Recommended for kids;Scenic view;Scuba gear;Significant hike;Snakes;Snowmobiles;Stealth required;Stroller accessible;Takes less than an hour;Telephone nearby;Thorns;Ticks;Watch for livestock;Wheelchair accessible"
$form = Replace("%AttributeList%",$AttributeList,$form)

$NotAttributeList = "Available at all times;Available during winter;Bicycles;Campfires;Camping available;Difficult climbing;Dogs;Drinking water nearby;Horses;Motorcycles;Off-road vehicles;Parking available;Picnic tables nearby;Poison plants;Public restrooms nearby;Quads;Recommended at night;Recommended for kids;Scenic view;Significant hike;Snowmobiles;Stealth required;Stroller accessible;Takes less than an hour;Telephone nearby;Wheelchair accessible"
$form = Replace("%NotAttributeList%",$NotAttributeList,$form)

WHILE TRUE
	$tmpS = Form($form,"")
	#MSGOK msg=$Attributes
	GOSUB name=DoFilter
	CANCEL
ENDWHILE


BEGINSUB name=DoFilter
IF Len($Attributes) > 0
	$count = RegExCount(";",$Attributes) + 1
	$x = 1
	$_sql = ""
	WHILE $x <= $count
		$name = Extract($Attributes,";",$x)
		IF Len($_sql) > 0
			$_sql = $_sql + "INTERSECT "
		ENDIF
		$_sql = $_sql + "SELECT aCode FROM AttName WHERE aName = '$name' AND aInc "
		$x = $x + 1
	ENDWHILE
ENDIF

IF Len($NotAttributes) > 0
	$count = RegExCount(";",$NotAttributes) + 1
	$x = 1
	WHILE $x <= $count
		$name = Extract($NotAttributes,";",$x)
		IF Len($_sql) > 0
			$_sql = $_sql + "INTERSECT "
		ENDIF
		$_sql = $_sql + "SELECT aCode FROM AttName WHERE aName = '$name' AND NOT aInc "
		$x = $x + 1
	ENDWHILE
ENDIF

#MSGOK msg=$_sql
$codes = Sqlite("sql",$_sql)
MACROFLAG type=clear range=all
$match = CodeMatch($codes,$_CrLf,"M")
IF $Invert
	MFILTER expression=not($d_MacroFlag)
	$message = "NOT ( "
ELSE
	MFILTER expression=$d_MacroFlag
	$message = ""
ENDIF
IF Len($Attributes) >0
	$message = $message + Replace(";"," AND ",$Attributes)
ENDIF
IF Len($NotAttributes) >0
 $message = $message + " NOT " + Replace(";"," AND ",$NotAttributes)
ENDIF
IF $Invert
	$message = $message + " )"
ENDIF
IF $_FilterCount = 0
	IF Len($Attributes) + Len($NotAttributes) > 0
		MSGOK Msg=Sorry no matching records with $message
	ENDIF
ELSE
	MSGOK Msg=Filtered to $message
ENDIF


ENDSUB



<Data> VarName=$form
#********************************************************************
# Form generated by GSAK form designer on Fri 06-Nov-2009 19:09:32
#********************************************************************

Name = Form1
  Type = Form
  Height = 585
  Width = 619

Name = Attributes
  Type = Checklistbox
  Columns = 4
  Height = 191
  Left = 21
  Top = 79
  Values = %AttributeList%
  Width = 561
  Taborder = 10

Name = NotAttributes
  Type = Checklistbox
  Columns = 4
  Height = 192
  Left = 21
  Top = 310
  Values = %NotAttributeList%
  Width = 561
  Taborder = 8

Name = Filter
  Type = Button
  Height = 25
  Left = 323
  Top = 510
  Width = 75
  Taborder = 9

Name = Label1
  Type = Label
  Height = 20
  Left = 183
  Size = 12
  Top = 56
  Width = 237
  Caption = Attributes Required to be Present

Name = Label2
  Type = Label
  Height = 20
  Left = 165
  Size = 12
  Top = 286
  Width = 273
  Caption = Attributes Required to be NOT Present

Name = Label3
  Type = Label
  Color = 16744448
  Height = 20
  Left = 243
  Size = 12
  Style = bold
  Top = 17
  Width = 116
  Caption = Attribute Filter

Name = Invert
  Type = Checkbox
  Height = 17
  Left = 204
  Top = 515
  Width = 97
  Taborder = 11
  Caption = Invert Filter

<enddata>






