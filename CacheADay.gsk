#*******************************************
# MacVersion = 1.1.0
# MacDescription = Check finds for A Cache Every Day (GC1N2Q6)
# MacAuthor = mokeefe
# MacFileName = CacheADay.gsk
# MacUrl =
#*******************************************

Option Explicit=Yes

Declare Var=$Output Type=String
Declare Var=$JDate Type=Numeric
Declare Var=$TempStart Type=Date
Declare Var=$Count Type=Numeric
Declare Var=$Temp1 Type=String
Declare Var=$Memo1 Type=String
Declare Var=$FormExit Type=String
Declare Var=$FoundDate Type=String
Declare Var=$DaysFound Type=Numeric
Declare Var=$DBs Type=String
Declare Var=$SettingsFile Type=String
Declare Var=$Data Type=String
Declare Var=$Combobox1 Type=String
Declare Var=$Combobox2 Type=String
Declare Var=$Checkbox1 Type=Boolean

#Initialize Variables
$Output=""
$TempStart = StringToDate("20031231")
$Count=1
$DaysFound = 0

$SettingsFile=$_Install + "\Macros\CacheADay.dat"
$DBs=Sysinfo("databases")

IF FileExists($SettingsFile)
    $Data = GetFile($SettingsFile)
    IF Left($Data,7) = "*Error*"
        Pause Msg="$Data"
        Cancel
    ENDIF
    $Combobox1=Extract($Data,";",1)
    $Combobox2=Extract($Data,";",2)
    IF RegExCount(";", $Data) >= 3
    	$Checkbox1=StrToBool(Extract($Data,";",3))
    ENDIF
ENDIF

IF $Checkbox1 = True
     $Form = EditForm($Form,"Label2","Enabled","Yes")
     $Form = EditForm($Form,"Combobox2","Enabled","Yes")
ENDIF

WHILE TRUE
    $FormExit = form($Form,"")
    BeginCase
        Case $FormExit = "SystemExit"
            CANCEL Msg="Macro Cancelled"

        Case $FormExit = "OK"
            GOSUB Name=Run
            BREAK 

        Case $FormExit = "Cancel"
           CANCEL Msg="Macro Cancelled"

        Case $FormExit = "Checkbox1"
	BEGINCASE
	     CASE $Checkbox1 = True
	          $Form = EditForm($Form,"Label2","Enabled","Yes")
	          $Form = EditForm($Form,"Combobox2","Enabled","Yes")
	     CASE $Checkbox1 = False
	          $Form = EditForm($Form,"Label2","Enabled","No")
	          $Form = EditForm($Form,"Combobox2","Enabled","No")
	ENDCASE
    ENDCASE 
ENDWHILE    

BEGINSUB Name=Run

$Data = PutFile($SettingsFile,$Combobox1 + ";" + $Combobox2 + ";" + BoolToStr($Checkbox1, "True", "False") + ";")
IF Left($Data,7) = "*Error*"
    PAUSE Msg="$Data"
    CANCEL 
ENDIF

# Initialize Array sizes
Array(1,0) = "366"
Array(2,0) = "366"
Array(3,0) = "366"

# Init the Date display Array
WHILE $Count <= 366
      $Temp1 = Substr(DateToString($TempStart + $Count), 5, 0)
      Array(3, $Count) = Substr($Temp1, 1, 2) + "/" + Substr($Temp1, 3, 2)
      $Count = $Count + 1
ENDWHILE
$Count=1

# Select the Found database
DATABASE Name=$Combobox1 Action=Select

GOTO Position=Top
WHILE not($_EoL)
      $FoundDate = DateToString($d_FoundByMeDate)
      # Check to make sure there is a valid found date
      IF Substr($FoundDate, 1, 4) <> "1899"
            $JDate = DateDiff($TempStart, StringToDate("2004" + Substr($FoundDate, 5, 0)))
            #Sanity Check
            IF $JDate > 0 AND $JDate < 377
	      Array(1, $JDate) = NumToStr(Val(Array(1, $JDate)) + 1)
            ENDIF
      ENDIF
      GOTO Position=Next
ENDWHILE

#Debug Status=on
IF $Checkbox1 = True
     # Select the Placed database
     DATABASE Name=$Combobox2 Action=Select

     GOTO Position=Top
     WHILE not($_EoL)
           $FoundDate = DateToString($d_PlacedDate)
           # Check to make sure there is a valid date
           IF Substr($FoundDate, 1, 4) <> "1899"
                 $JDate = DateDiff($TempStart, StringToDate("2004" + Substr($FoundDate, 5, 0)))
                 #Sanity Check
                 IF $JDate > 0 AND $JDate < 377
	           Array(2, $JDate) = NumToStr(Val(Array(2, $JDate)) + 1)
                 ENDIF
           ENDIF
           GOTO Position=Next
     ENDWHILE
ENDIF

# Fill the ouput string
$Output = "Date" + $_Tab + "Found" + $_Tab + "Placed" + $_NewLine + "------------------------------------------" + $_NewLine
WHILE $Count <= 366
      $Output=$Output + Array(3, $Count) + $_Tab + Array(1, $Count) + $_Tab + Array(2, $Count) + $_NewLine
      IF Val(Array(1, $Count)) > 0 OR Val(Array(2, $Count)) > 0
	$DaysFound = $DaysFound + 1
      ENDIF
      $Count = $Count +1
ENDWHILE
$Output = "Days Found: " + NumToStr($DaysFound) + ", Days Left: " + NumToStr(366 - $DaysFound) + $_NewLine + $_NewLine + $Output


#Assign output to results form
$Memo1=$Output

# Display results form
WHILE TRUE
    $FormExit = form($ResultForm,"")
    BeginCase
        Case $FormExit = "SystemExit"
        BREAK 

        Case $FormExit = "OK"
        BREAK 

        Case $FormExit = "Clipboard"
        CLIP Data=$Output
        BREAK 
    EndCase
ENDWHILE    

ENDSUB

<Data> VarName=$Form
#********************************************************************
# Form generated by GSAK form designer
#********************************************************************

Name = Form1
  Type = Form
  Caption = Cache A Day Report
  Height = 180
  Width = 500

Name = Label1
  Type = Label
  Height = 13
  Left = 18
  Top = 16
  Width = 264
  Caption = Select the database that contains your FOUND caches:

Name = Label2
  Type = Label
  Height = 13
  Enabled=No
  Left = 18
  Top = 60
  Width = 248
  Caption = Select the database that contains your PLACED caches:

Name = Combobox1
  Type = Combobox
  Height = 21
  Left = 294
  Top = 12
  Values = $DBs
  Width = 187
  Taborder = 1

Name = Combobox2
  Type = Combobox
  Enabled=No
  Height = 21
  Left = 294
  Top = 55
  Values = $DBs
  Width = 187
  Taborder = 3

Name = OK
  Type = Button
  Height = 25
  Left = 85
  Top = 90
  Width = 75
  Taborder = 4
  Caption = OK

Name = Cancel
  Type = Button
  Height = 25
  Left = 331
  Top = 90
  Width = 75
  Taborder = 5
  Caption = Cancel

Name = Checkbox1
  Type = Checkbox
  Exitonchange = Yes
  Height = 17
  Left = 18
  Top = 40
  Width = 15
  Taborder = 2

Name = Label3
  Type = Label
  Height = 13
  Left = 40
  Top = 40
  Width = 127
  Caption = Include PLACED database

<enddata>

<Data> VarName=$ResultForm
#********************************************************************
# Form generated by GSAK form designer on Sun 27-Jan-2008 20:01:53
#********************************************************************

Name = ResultForm
  Type = Form
  Caption = Cache Finds Per Day:
  Color = 15457491
  Height = 407
  Width = 226

Name = Memo1
  Type = Memo
  Height = 307
  Left = 12
  Scrollbars = Vertical
  Top = 12
  Width = 193
  Taborder = 8

Name = OK
  Type = Button
  Height = 25
  Left = 18
  Top = 336
  Width = 75
  Taborder = 9
  Caption = OK

Name = Clipboard
  Type = Button
  Height = 25
  Left = 128
  Top = 336
  Width = 75
  Taborder = 10
  Caption = Clipboard

<enddata>

