#*******************************************
# MacVersion = 20100625
# MacDescription = Create Filters or Save Files From GSAK Google Maps Polygon / Route Utility
# MacAuthor = Pdops
# MacFileName = GooglePolyEditorFilter.gsk
# MacUrl =http://gsak.net/board/index.php?showtopic=8574&view=getlastpost
#*******************************************

# Change Log

# 20080606 added options line/route filter
# 20080608 added options for GPX output to file
# 20080609 accepts TSV data, warn if GPX output is polygon or > 500 points. added help page
# 20090115 added option to save filters to GUI dropdown list
# 20100624 added option to center/zoom map and macro settings are saved at exit
# 20100625 extract folder from file path to save with macro settings at exit

###############################################################################################

VERCHECK Version=7.7.1.34(You can find the latest version of GSAK in the forums at http://support.gsak.net/.)

# ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

# Default values usually only used the first time the macro is run

$FileFolder=$_AppData + "\macros"

$cbOpenWebPage = True

$cbCenterZoom = True

$cbFont="10"

# ++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

# Show version number in Form caption

$MacroVersion=MacHeader("MacVersion")

$Form = EditForm($Form,"Form1","Caption","Create  Arc / Poly Filters or GPX Files From Google Maps v$MacroVersion")

# restore saved settings from xml file

MacSettings Type=R FileCheck=N

# Define dropdown list of databases to match users

$dblist = SysInfo("databases")

# Set dropdown list of databases to Current database

$DLdatabases=$_CurrentDatabase


# Clear Clipboard

CLIP DATA=""

#Create List for data from clipboard

$List=List("CPs","Create","$_NewLine")

# Initialize file saved flag

$SaveFlag="No"

If $cbOpenWebPage = True

	$form = EditForm($form,"cbCenterZoom","Enabled","Yes")

Else

	$form = EditForm($form,"cbCenterZoom","Enabled","No")

EndIf

##################

# Display main form

While True # Infinite loop to redisplay form as required

$FormExit = form($form,"")

BeginCase

    Case $FormExit = "SystemExit"

      MacSettings Type=S Vars=FileFolder,cbOpenWebPage,cbCenterZoom,cbFont

      Cancel

    Case $FormExit = "btnCancel"

      MacSettings Type=S Vars=FileFolder,cbOpenWebPage,cbCenterZoom,cbFont

      Cancel

    Case $FormExit = "btnHelp"

	GOSUB Name=DisplayHelpScreen

    Case $FormExit = "btnRUN"

	If $cbOpenWebPage = True

		MsgOK msg=Instructions are displayed in GSAK $_Newline $_Newline After The Browser Window Opens

		If $cbCenterZoom = True

			$_SQL="Select Min(Latitude),Max(Longitude),Max(Latitude),Min(Longitude) From caches Where $_Where"

			$data=SQLite("SQL","$_SQL","Headings=No")

			$minlat=extract($data,";",1)
			$minlon=extract($data,";",2)
			$maxlat=extract($data,";",3)
			$maxlon=extract($data,";",4)

			$url="http://gsak.net/google/polygoneditor.html?minlat=$minlat&minlon=$minlon&maxlat=$maxlat&maxlon=$maxlon"

			Web URL=$url

		Else

			WEB URL="http://gsak.net/google/polygoneditor.html"

		EndIf

	EndIf

	GoSub name=DisplayINSTRUCTIONS

EndCase

# Change database

If $_CurrentDatabase <> $DLdatabases

	DATABASE Name=$DLdatabases

EndIf

If $cbOpenWebPage = True

	$form = EditForm($form,"cbCenterZoom","Enabled","Yes")

Else

	$form = EditForm($form,"cbCenterZoom","Enabled","No")

EndIf

EndWhile

##  Subroutines  #####################################################################################

BeginSub name=DisplayINSTRUCTIONS

CANCELFILTER

$MessageText = "$INSTRUCTIONS"


While true # Infinite loop to redisplay form as required

	$message = EditForm($message,"messageText","Size","$cbFont")

	$FormExit = form($message,"")

	If $FormExit = "SystemExit"

		Break

	EndIf

	# Save the form's current position before we close it
	$message = EditForm($message,"message1","Top",$_FormTop)
	$message = EditForm($message,"message1","Left",$_FormLeft)

 
	If $btnOK

		If IsEmpty(GetClipText)

			GoSub name=TryAgainOrExit

			GoSub name=DisplayINSTRUCTIONS
	
		Else

			GoSub name=SetPolyFilter

		EndIf

		ExitSub

	EndIf
 

EndWhile


EndSub

# ***********************************************************************************************

BeginSub name=SetPolyFilter

	# Get contents of clipboard

	$Output = GetClipText()

	# Replace Tabs with commas to convert TSV to CSV

	$Output = replace(chr(9),",",$Output,true)

	# Test $Output to determine type line or polygon, CSV or GPX

	GoSub name=DetermineDataType

	# Replace carriage returns with tildes for format required by Arc/Poly Filter

	$PolyCoords=Replace("$_NewLine", "~", $Output, true)

	If $TypeFlag="CSVpolygon"

		# Insert coordinates into <data>statement containing filter settings

		$Filter=Replace("InsertPolyCoordsHere", $PolyCoords, $FilterOrg, true)
	EndIf

	If $TypeFlag="CSVline"

		$Filter=Replace("InsertPolyCoordsHere", $PolyCoords, $FilterOrg, true)

		$Filter=Replace("edtArcDistance=", $LineDistance, $Filter, true)

		$Filter=Replace("rbtArc=False", "rbtArc=True", $Filter, true)

		$Filter=Replace("rbtPoly=True", "rbtPoly=False", $Filter, true)

	EndIf

	# Set filter using coordinates from clipboard

	MacroSet Dialog=Filter VarName=$filter
	Filter Name=<macro>

	Speedmode Status=off

	GoSub name=ContinueOrExit

EndSub

# ***********************************************************************************************

BeginSub name=TryAgainOrExit   #clipboard was empty

While True # Infinite loop to redisplay form as required

$YesNo = form($FormYesNo,"")

BeginCase

    Case $YesNo = "SystemExit"

      MacSettings Type=S Vars=FileFolder,cbOpenWebPage,cbCenterZoom,cbFont

      Cancel

    Case $YesNo = "ExitYN"

      MacSettings Type=S Vars=FileFolder,cbOpenWebPage,cbCenterZoom,cbFont

      Cancel

    Case $YesNo = "TryAgain"

	Break

EndCase

EndWhile

EndSub

# *********************************************************************************************

BeginSub name=ContinueOrExit  # filter set or file saved - continue?

If $TypeFlag="GPXline" OR $TypeFlag="GPXpolygon"

	# Edit form label
	$FormContinue=EditForm($FormContinue,"Label1","Caption","The GPX file has been saved as")

	# Edit form label to not show caches returned by filter
	$FormContinue=EditForm($FormContinue,"Label2","Visible","No")

	# Edit form label to show filepath
	$FormContinue=EditForm($FormContinue,"Label3","Caption","$FilePath")

	# Edit form label to not show Save Filter button
	$FormContinue=EditForm($FormContinue,"btnSaveFilter","Visible","No")

Else

	# Edit form label to show caches returned by filter
	$FormContinue=EditForm($FormContinue,"Label1","Caption","The filter has been set and returned")

	# Edit form label to show caches returned by filter
	$FormContinue=EditForm($FormContinue,"Label2","Visible","Yes")

	# Edit form label to show caches returned by filter
	$FormContinue=EditForm($FormContinue,"Label2","Caption","$_FilterCount Geocaches")

	# Edit form label to show caches returned by filter
	$FormContinue=EditForm($FormContinue,"Label3","Caption","To name and save this filter press Save Filter")

	# Edit form button
	$FormContinue=EditForm($FormContinue,"btnSaveFilter","Visible","Yes")

EndIf

While True # Infinite loop to redisplay form as required

$Continue = form($FormContinue,"")

BeginCase

    Case $Continue = "SystemExit"

      MacSettings Type=S Vars=FileFolder,cbOpenWebPage,cbCenterZoom,cbFont

      Cancel

    Case $Continue = "btnSaveFilter"

	GoSub name=SaveFilter

    Case $Continue = "ExitContinue"

      MacSettings Type=S Vars=FileFolder,cbOpenWebPage,cbCenterZoom,cbFont

      Cancel

    Case $Continue = "PolyExisting"

	CLIP DATA=""

	GoSub name=DisplayINSTRUCTIONS

    Case $Continue = "PolyNew"

	CLIP DATA=""

		If $cbCenterZoom = True

			$_SQL="Select Min(Latitude),Max(Longitude),Max(Latitude),Min(Longitude) From caches Where $_Where"

			$data=SQLite("SQL","$_SQL","Headings=No")

			$minlat=extract($data,";",1)
			$minlon=extract($data,";",2)
			$maxlat=extract($data,";",3)
			$maxlon=extract($data,";",4)

			$url="http://gsak.net/google/polygoneditor.html?minlat=$minlat&minlon=$minlon&maxlat=$maxlat&maxlon=$maxlon"

			Web URL=$url

		Else

			WEB URL="http://gsak.net/google/polygoneditor.html"

		EndIf

	GoSub name=DisplayINSTRUCTIONS

EndCase

EndWhile

EndSub

# *********************************************************************************************

BeginSub name=SaveFilter  # save set fiter

While True # Infinite loop to redisplay form as required

$FilterB = form($SaveFilterForm,"")

BeginCase

    Case $FilterB = "SystemExit"

      MacSettings Type=S Vars=FileFolder,cbOpenWebPage,cbCenterZoom,cbFont

      Cancel

    Case $FilterB = "ExitFilter"

      MacSettings Type=S Vars=FileFolder,cbOpenWebPage,cbCenterZoom,cbFont

      Cancel

    Case $FilterB = "btnSaveFilter"

	$MsgFilter= "The filter named " + $_Quote + " $FilterName " + $_Quote + " has been saved and will"
	$MsgFilter= $MsgFilter + $_NewLine + "now appear in the in the dropdown list of filters."

	$Filters=SysInfo("Filters")

	$Filters=$Filters +";"

	$FName= ";" + "$FilterName" + ";"

	# Check if filter name exists

	If RegEx($FName,$Filters)

		GOSUB Name=FilterNameExits

	Else

		MacroSet Dialog=Filter VarName=$filter Name=$FilterName
		MsgOK msg=$MsgFilter

	EndIf

EndCase

Break

EndWhile

EndSub

# *********************************************************************************************

BEGINSUB Name=FilterNameExits

While true

$FormExit = form($FilterNameYesNoForm,"")

# Save the screen position of the form for later
$FilterNameYesNoForm = EditForm($FilterNameYesNoForm,"FilterYesNo","Top",$_FormTop)
$FilterNameYesNoForm = EditForm($FilterNameYesNoForm,"FilterYesNo","Left",$_FormLeft)

If $FormExit = "SystemExit"
   break
EndIf

BeginCase

	Case $BtnYes
		MacroSet Dialog=Filter VarName=$filter Name=$FilterName
		MsgOK msg=$MsgFilter
		ExitSub

	Case $BtnNo
		ExitSub

EndCase

EndWhile

ENDSUB

# *********************************************************************************************
BeginSub name=DetermineDataType

$TypeFlag=""

# Populate List with Coordinates from clipboard

$List=List("CPs","Replace",$Output)
$Count=List("CPs","Count","")
$TotalPoints = val($Count) - 6 # total lines minus header and footer

# Get Content of different Lines of data in List

$First=List("CPs","Item","1") # First Line of data

$LastCP=val($Count) - 1 # Line count of last coordiante pair in CSV data (totals lines - 1)

$Last=List("CPs","Item","$LastCP") # Line with last coordiante pair in CSV data

$Fifth=List("CPs","Item","5") # fifth line first coordinate pair in GPX file

$LastRCP=val($Count) - 2 # Line count of last coordiante pair in GPX data (totals lines - 2)

$LastGPX=List("CPs","Item","$LastRCP") # Line with last coordinate pair in GPX data

# Determine data type based on line content

If Left($First,5)="<?xml" # GPX file because first line starts with "<?xml"

	If $TotalPoints > 499

		MsgOK msg=$TotalPoints Total Points. Routes must be less than 500 points to work on Geocaching.com

	EndIf

	If $Fifth=$LastGPX # it's a polygon First and last coordiantes pairs match in GPX file

		$TypeFlag="GPXpolygon"
		MsgOK msg= The 'Caches along a Route' feature on Geocaching.com $_Newline will accept polygons but treats them as lines.
		GoSub name=SetGPXname
		ExitSub

	EndIf

	$TypeFlag="GPXline"

	GoSub name=SetGPXname

	ExitSub

EndIf

If $First=$Last # it's a CSV polygon  First and last coordinates pairs match in CSV file

	$TypeFlag="CSVpolygon"
	ExitSub

Else  # it's a CSV line

	$TypeFlag="CSVline"
	GoSub name=SetLineDistance
EndIf

EndSub

# *********************************************************************************************

BeginSub name=SetLineDistance

$LineDistance=""

If sysinfo("distance") = "K"

	# Edit form label for metric units
	$LineForm=EditForm($LineForm,"LabelDistance","Caption","Distance in Km (for 500m enter .5)")

Else
	# Edit form label for standard units
	$LineForm=EditForm($LineForm,"LabelDistance","Caption","Distance in Miles (.5 =half a mile)")

Endif

While True # Infinite loop to redisplay form as required

$Continue = form($LineForm,"")

BeginCase

    Case $Continue = "SystemExit"

      MacSettings Type=S Vars=FileFolder,cbOpenWebPage,cbCenterZoom,cbFont

      Cancel

    Case $Continue = "btnExitDist"

      MacSettings Type=S Vars=FileFolder,cbOpenWebPage,cbCenterZoom,cbFont

      Cancel

    Case $Continue = "btnProceed"

		If val($LineDistance) > 0

			$LineDistance="edtArcDistance=$LineDistance"

		Else
	
			MsgOK msg=Please set a distance

			GoSub name=DetermineDataType		

		EndIf

		ExitSub

EndCase

EndWhile

EndSub

# **********************************************************************************************

BeginSub name=SetGPXname

$Date=DateToString($_today)

If $TypeFlag="GPXline"

	$FileDefault="\Line$Date.gpx"

Else

	$FileDefault="\Polygon$Date.gpx"

EndIf

If $SaveFlag <> "Yes"

	$FilePath="$FileFolder" + "$FileDefault"

EndIf

While True # Infinite loop to redisplay form as required

$Continue = form($GPXfileForm,"")

BeginCase

    Case $Continue = "SystemExit"

      MacSettings Type=S Vars=FileFolder,cbOpenWebPage,cbCenterZoom,cbFont

      Cancel

    Case $Continue = "btnExitGPX"

      MacSettings Type=S Vars=FileFolder,cbOpenWebPage,cbCenterZoom,cbFont

      Cancel

    Case $Continue = "btnSave"

	If $FileFolder=""

		MsgOK msg=Please specify a file name

		GoSub name=DetermineDataType

	Else

		$Work=PutFile($FilePath, $Output)

		$SaveFlag="Yes"

		# Extract folder from filepath
		$FPcount=RegExCount("\\",$FilePath)
		$FPposition=RegExPos("\\",$FilePath,$FPcount)
		$FileFolder=left($FilePath,$FPposition - 1)

		

		GoSub name=ContinueOrExit

	EndIf

EndCase

EndWhile

EndSub

# **********************************************************************************************

BEGINSUB Name=DisplayHelpScreen

$HelpText = "$HelpText" + $_CrLf + "$INSTRUCTIONS"

While true

	$help = EditForm($help,"HelpText","Size","$cbFont")

	$FormExit = form($help,"")

	If $FormExit = "SystemExit"

		Break

	EndIf

	# Save the form's current position before we close it
	$help = EditForm($help,"help1","Top",$_FormTop)
	$help = EditForm($help,"help1","Left",$_FormLeft)

	If $btnCopy

		Clip data=$HelpText

	EndIf
 
	If $btnClose

		ExitSub

	EndIf
 

EndWhile

ENDSUB

# **********************************************************************************************

#### Data Statements

<data> VarName=$FilterOrg
edtArcDistance=
chkArcExclude=False
rbtArc=False
rbtPoly=True
rbtPoint=False
rbtReplace=True
rbtAppend=False

ArcFilter=InsertPolyCoordsHere

<enddata>

<data> VarName=$HelpText
Create  Arc / Poly Filters or GPX Files Using The Output From The GSAK Google Maps Polygon / Route Utility

This macro will semi-automatically transfer the CSV or TSV output of a line or polygon defined in the utility to the Arc / Poly filter dialog and set an arc / line or poly filter for the selected database. Optionally the filter can be named and saved to appear in the dropdown list of filters.

GPX output will be saved in the GPX (v1.1) file format for use with the 'Caches along a Route' feature on Geocaching.com

Features include:

- change databases

- opens the GSAK Google Maps Polygon / Route Utility web page with option to center/zoom on bounds of current database.

- if you have forgotten to copy the the coordinates you are prompted to do so

- automatically determines the type of output copied from the output window of the web page.

- CSV (Comma Separated Values) and TSV (Tab Separated Values) output is inserted into the Arc/Poly filter dialog and the filter set. Optionally the filter can be named and saved to appear in the dropdown list of filters.

- GPX output is saved to file and you are notified if the size exceeds the 500 point limit of 'Caches along a Route' feature on Geocaching.com. A default file name incorporating the date is saved to the 'macros' folder in the GSAK install directory.  The path and file name can be changed in the 'save' dialog box and this selected path will be remembered while in the current session (until the macro is closed).

- after the filter has been set or file saved you are given the option to continue or exit

- macro settings are changed at exit.

NOTES:

A polygon saved to a GPX file will be treated as a line when used in the 'Caches along a Route' feature on Geocaching.com

When saving filters you are prompted if the filter name already exists and are given the option to over write the existing filter.

IMPORTANT: You must MANUALLY copy the data from the output window of the web page to the Windows clipboard as detailed in steps 5 & 6 of the instructions below.
<enddata>

<data> VarName=$INSTRUCTIONS
INSTRUCTIONS:

Complete a Polygon or Line in the GSAK Google Maps Polygon / Route Utility web page as outlined below:

1. Click 'Start Polygon' or 'Start Line" then click on the map to add points.

2. Click again on the last point to close polygon or complete the line.

3. To edit once closed, mouse over and drag the points. Click on a point to delete it.

4. Set the Output to CSV or TSV for GSAK filters or to GPX to save the data to a GPX file.

5. Highlight the coordinate pairs in the Output Window located to the right of the map by clicking in the Output Window (all text automatically gets selected)

6. Copy the Coordinates to the Clipboard by right clicking anywhere in the Output Window and selecting Copy or use Ctrl-C.

7. RETURN TO THE GSAK PROGRAM WINDOW AND PRESS THE "GO" BUTTON ON THIS MACRO FORM TO RETRIEVE THE POINTS FROM THE CLIPBOARD AND PROCESS THE OUTPUT.


The coordinate pairs from The GSAK Google Maps Polygon / Route Utility are processed as follows;

Polygon output in CSV or TSV format will result in the setting of a Poly Filter which can be saved if desired.

Line output in CSV or TSV format will result in the setting of an Arc/Line Filter (you will be be prompted to specify a distance from the line to search)

Polygon and Line output in GPX format will be saved in a GPX file (you will be prompted for a file name).

A status message is then shown and you're given the option to exit or continue using the existing browser window (to edit the existing polygon or line) or open a new one.
<enddata>

########   Forms  ##############################################################################

<Data> VarName=$form
#********************************************************************
# Form generated by GSAK form designer on Thu 24-Jun-2010 11:06:35
#********************************************************************

Name = Form1
  Type = Form
  Caption = replaced by EditForm()
  Height = 350
  Width = 500

Name = Label1
  Type = Label
  Color = 16711680
  Height = 29
  Left = 64
  Size = 15
  Style = bold
  Top = 5
  Width = 463
  Caption = Create  Arc / Poly Filters or GPX Files

Name = Label4
  Type = Label
  Color = 16711680
  Height = 29
  Left = 39
  Size = 15
  Style = bold
  Top = 53
  Width = 524
  Caption = GSAK Google Maps Polygon / Route Utility

Name = DLdatabases
  Type = Combobox
  Color = 16777215
  Display = 12
  Exitonchange = Yes
  Height = 25
  Left = 193
  Top = 199
  Values = $dblist
  Width = 187
  Taborder = 9

Name = Label10
  Type = Label
  Height = 22
  Left = 112
  Size = 11
  Style = bold
  Top = 200
  Width = 91
  Caption = Database:

Name = Label5
  Type = Label
  Color = 4194368
  Height = 16
  Left = 74
  Size = 8
  Style = bold
  Top = 232
  Width = 444
  Caption = Open The GSAK Google Maps Polygon / Route Utility Web Page

Name = btnRUN
  Type = Button
  Height = 25
  Left = 44
  Top = 288
  Width = 75
  Taborder = 8
  Caption = RUN

Name = Label6
  Type = Label
  Height = 18
  Left = 53
  Size = 9
  Style = bold
  Top = 89
  Width = 458
  Caption = Featuring semi-automatic transfer of data from the utility with

Name = Label7
  Type = Label
  Height = 18
  Left = 44
  Size = 9
  Style = bold
  Top = 105
  Width = 470
  Caption = detection of type / format the appropriate Arc / Line or Polygon

Name = btnCancel
  Type = Button
  Height = 25
  Left = 372
  Top = 288
  Width = 75
  Taborder = 10
  Caption = Cancel

Name = Label8
  Type = Label
  Color = 255
  Height = 20
  Left = 73
  Size = 10
  Style = bold
  Top = 173
  Width = 427
  Caption = Note That Any Existing Set Filter Will Be Canceled

Name = Label2
  Type = Label
  Color = 16711680
  Height = 26
  Left = 129
  Size = 13
  Style = bold
  Top = 32
  Width = 293
  Caption = Using The Output From The

Name = Label3
  Type = Label
  Height = 18
  Left = 47
  Size = 9
  Style = bold
  Top = 121
  Width = 469
  Caption = filter dialog is set or file saved (in GPX v1.1 format suitable for

Name = Label11
  Type = Label
  Height = 18
  Left = 38
  Size = 9
  Style = bold
  Top = 137
  Width = 490
  Caption = use with the 'Caches along a Route' feature on Geocaching.com)

Name = btnHelp
  Type = Button
  Height = 25
  Left = 208
  Top = 288
  Width = 75
  Taborder = 11
  Caption = Help

Name = cbOpenWebPage
  Type = Checkbox
  Exitonchange = Yes
  Height = 17
  Left = 48
  Top = 231
  Width = 15
  Taborder = 12

Name = cbCenterZoom
  Type = Checkbox
  Enabled = No
  Exitonchange = No
  Fontstyle = bold
  Height = 17
  Left = 63
  Top = 253
  Width = 409
  Taborder = 13
  Caption = Center/Zoom Map On Bounds Of Current Database

<enddata>






<Data> VarName=$FormYesNo
#********************************************************************
# Form generated by GSAK form designer on Fri 06-Jun-2008 12:11:51
#********************************************************************

Name = FormYesNo
  Type = Form
  Color = 65535
  Height = 172
  Width = 326
  Caption = The Clipboard Is Empty! No filter can be set!

Name = TryAgain
  Type = Button
  Height = 25
  Left = 65
  Top = 96
  Width = 75
  Taborder = 8
  Caption = Try Again

Name = ExitYN
  Type = Button
  Height = 25
  Left = 177
  Top = 97
  Width = 75
  Taborder = 9
  Caption = EXIT

Name = Label1
  Type = Label
  Height = 15
  Left = 18
  Size = 9
  Style = bold
  Top = 15
  Width = 281
  Caption = The Clipboard Is Empty! No filter can be set!

Name = Label2
  Type = Label
  Height = 13
  Left = 27
  Style = bold
  Top = 39
  Width = 263
  Caption = Please Copy The Points In the Output Window

Name = Label3
  Type = Label
  Height = 13
  Left = 95
  Style = bold
  Top = 65
  Width = 128
  Caption = Then Press 'Try Again'

<enddata>

<Data> VarName=$FormContinue
#********************************************************************
# Form generated by GSAK form designer on Tue 06-Jan-2009 12:34:00
#********************************************************************

Name = FormContinue
  Type = Form
  Caption = Save and Continue Or Exit?
  Color = 16777145
  Height = 201
  Width = 326

Name = PolyExisting
  Type = Button
  Height = 25
  Left = 17
  Top = 138
  Width = 75
  Taborder = 8
  Caption = Existing

Name = ExitContinue
  Type = Button
  Height = 25
  Left = 225
  Top = 138
  Width = 75
  Taborder = 9
  Caption = EXIT

Name = Label1
  Type = Label
  Height = 16
  Left = 34
  Size = 10
  Style = bold
  Top = 8
  Width = 245
  Caption = The filter has been set and returned

Name = Label2
  Type = Label
  Height = 16
  Left = 106
  Size = 10
  Style = bold
  Top = 26
  Width = 105
  Caption = nnn geocaches

Name = Label3
  Type = Label
  Height = 13
  Left = 30
  Style = bold
  Top = 48
  Width = 257
  Caption = To name and save this filter press Save Filter

Name = Label5
  Type = Label
  Height = 13
  Left = 13
  Style = bold
  Top = 98
  Width = 292
  Caption = To build another polygon or line in the existing or in

Name = Label6
  Type = Label
  Height = 13
  Left = 20
  Style = bold
  Top = 117
  Width = 277
  Caption = a new browser window select from buttons below

Name = PolyNew
  Type = Button
  Height = 25
  Left = 123
  Top = 138
  Width = 75
  Taborder = 10
  Caption = New

Name = btnSaveFilter
  Type = Button
  Height = 25
  Left = 121
  Top = 67
  Width = 75
  Taborder = 11
  Caption = Save Filter

<enddata>

<Data> VarName=$LineForm
#********************************************************************
# Form generated by GSAK form designer on Sun 08-Jun-2008 13:23:04
#********************************************************************

Name = LineForm
  Type = Form
  Caption = Specify Distance
  Height = 182
  Width = 356

Name = LineDistance
  Type = Edit
  Height = 21
  Left = 20
  Top = 39
  Width = 33
  Taborder = 8

Name = LabelDistance
  Type = Label
  Height = 13
  Left = 67
  Style = bold
  Top = 43
  Width = 81

Name = Label2
  Type = Label
  Height = 13
  Left = 37
  Style = bold
  Top = 71
  Width = 274
  Caption = GSAK will create a filter that includes all caches

Name = Label3
  Type = Label
  Height = 13
  Left = 38
  Style = bold
  Top = 91
  Width = 271
  Caption = that fall within this distance from the arc or line.

Name = btnProceed
  Type = Button
  Height = 25
  Left = 68
  Top = 118
  Width = 75
  Taborder = 9
  Caption = Set Distance

Name = btnExitDist
  Type = Button
  Height = 25
  Left = 205
  Top = 118
  Width = 75
  Taborder = 10
  Caption = Exit

Name = Label1
  Type = Label
  Color = 16711680
  Height = 18
  Left = 20
  Size = 11
  Style = bold
  Top = 9
  Width = 308
  Caption = Specify a Distance From The Arc Or Line

<enddata>

<Data> VarName=$GPXfileForm
#********************************************************************
# Form generated by GSAK form designer on Sat 07-Jun-2008 15:50:47
#********************************************************************

Name = GPXfileForm
  Type = Form
  Caption = Save as GPX (v1.1) File
  Height = 162
  Width = 356

Name = btnSave
  Type = Button
  Height = 25
  Left = 68
  Top = 102
  Width = 75
  Taborder = 8
  Caption = Save File

Name = btnExitGPX
  Type = Button
  Height = 25
  Left = 205
  Top = 102
  Width = 75
  Taborder = 9
  Caption = Exit

Name = FilePath
  Type = File
  Height = 21
  Left = 27
  Top = 72
  Width = 294
  Taborder = 10

Name = Label4
  Type = Label
  Height = 13
  Left = 25
  Style = bold
  Top = 30
  Width = 297
  Caption = 'Caches along a Route' feature on Geocaching.com.

Name = Label5
  Type = Label
  Height = 13
  Left = 23
  Style = bold
  Top = 10
  Width = 301
  Caption = The GPX (v1.1) file format is suitable for use with the

Name = Label1
  Type = Label
  Color = 16711680
  Height = 13
  Left = 30
  Top = 48
  Width = 287
  Caption = (routes need to be under 500 miles and less then 500 points.)

<enddata>

<Data> VarName=$SaveFilterForm
#********************************************************************
# Form generated by GSAK form designer on Tue 06-Jan-2009 14:00:51
#********************************************************************

Name = FilterName
  Type = Edit
  Height = 21
  Left = 27
  Top = 56
  Width = 209
  Taborder = 8

Name = SaveFilterForm
  Type = Form
  Caption = Name and Save Filter
  Height = 162
  Width = 275

Name = btnSaveFilter
  Type = Button
  Height = 25
  Left = 28
  Top = 95
  Width = 75
  Taborder = 9
  Caption = Save Filter

Name = btnExitFilter
  Type = Button
  Height = 25
  Left = 165
  Top = 95
  Width = 75
  Taborder = 10
  Caption = Exit

Name = Label5
  Type = Label
  Height = 20
  Left = 36
  Size = 12
  Style = bold
  Top = 18
  Width = 194
  Caption = Enter Filter Name Below

<enddata>

<Data> VarName=$FilterNameYesNoForm
#********************************************************************
# Form generated by GSAK form designer on Wed 07-Jan-2009 13:57:34
#********************************************************************

Name = FilterYesNo
  Type = Form
  Caption = Filter Exits
  Height = 153
  Width = 322

Name = YesNoFormLbl1
  Type = Label
  Color = 255
  Height = 25
  Left = 51
  Size = 16
  Style = bold
  Top = 13
  Width = 212
  Caption = Filter Already Exists

Name = YesNoFormLbl2
  Type = Label
  Height = 15
  Left = 10
  Size = 9
  Style = bold
  Top = 48
  Width = 294
  Caption = Do You Want To Overwrite The Existing Filter?

Name = BtnYes
  Type = Button
  Height = 25
  Left = 75
  Top = 79
  Width = 50
  Taborder = 8
  Caption = YES

Name = BtnNo
  Type = Button
  Height = 25
  Left = 189
  Top = 79
  Width = 50
  Taborder = 9
  Caption = NO

<enddata>

<Data> VarName=$help
#********************************************************************
# Form generated by GSAK form designer on Sun 21-Sep-2008 11:27:31
#********************************************************************

Name = help1
  Type = Form
  Caption = Help
  Color = 8454143
  Height = 584
  Width = 542

Name = btnClose
  Type = Button
  Escape = Yes
  Height = 20
  Left = 446
  Top = 3
  Width = 75
  Taborder = 8
  Caption = Close

Name = HelpText
  Type = Memo
  Color = 15987699
  Height = 519
  Left = 9
  Readonly = Yes
  Scrollbars = Vertical
  Size = 10
  Style = Bold
  Top = 26
  Width = 515
  Wrap = Yes
  Taborder = 9

Name = cbFont
  Type = Combobox
  Exitonchange = Yes
  Height = 21
  Left = 10
  Top = 2
  Values = 8;9;10;11;12;14
  Width = 64
  Taborder = 10

Name = Label1
  Type = Label
  Height = 16
  Left = 88
  Size = 10
  Style = bold
  Top = 4
  Width = 64
  Caption = Text Size

Name = btnCopy
  Type = Button
  Escape = Yes
  Height = 20
  Left = 206
  Top = 3
  Width = 155
  Taborder = 11
  Caption = Copy Help Text To Clipboard

<enddata>

<Data> VarName=$message
#********************************************************************
# Form generated by GSAK form designer on Sun 21-Sep-2008 11:27:31
#********************************************************************

Name = message1
  Type = Form
  Caption = Instructions
  Color = 8454143
  Height = 584
  Width = 542

Name = btnOK
  Type = Button
  Escape = Yes
  Height = 20
  Left = 446
  Top = 3
  Width = 75
  Taborder = 8
  Caption = GO

Name = MessageText
  Type = Memo
  Color = 15987699
  Height = 519
  Left = 9
  Readonly = Yes
  Scrollbars = Vertical
  Size = 10
  Style = Bold
  Top = 26
  Width = 515
  Wrap = Yes
  Taborder = 9

Name = cbFont
  Type = Combobox
  Exitonchange = Yes
  Height = 21
  Left = 10
  Top = 2
  Values = 8;9;10;11;12;14
  Width = 64
  Taborder = 10

Name = Label1
  Type = Label
  Height = 16
  Left = 88
  Size = 10
  Style = bold
  Top = 4
  Width = 64
  Caption = Text Size


<enddata>


