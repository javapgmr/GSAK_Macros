################################################################################
# MacVersion = 1.0
# MacDescription = Filter your database for Caches along a Route
# MacAuthor = gkrone
# MacFileName = CAAR2.gsk
# MacURL = 
################################################################################
#
#
#

#*******************************************************
#   Variable declarations for
#   J:\gsak\Macros\CAAR2.gsk
#
#   Generated 6/24/2009 on GSAKVariables.gsk Rev V0.31
#
#*******************************************************


Option Explicit=Yes

Declare Var=$DynamicFilter Type=String
Declare Var=$Filter Type=String
Declare Var=$frmMain Type=String
Declare Var=$intLoop Type=Numeric
Declare Var=$regex Type=String
Declare Var=$strFile Type=String
Declare Var=$strFilePath Type=String
Declare Var=$strFilter Type=String
Declare Var=$strKML Type=String
Declare Var=$strLat Type=String
Declare Var=$strLine Type=String
Declare Var=$strLon Type=String
Declare Var=$strPathFileList Type=String
Declare Var=$txtDistance Type=String
Declare Var=$strFileExtension Type=String
DECLARE Var=$RouteDir Type=String

$RouteDir = "\Macros\routeFiles"
$txtDistance = ".5"
$strPathFileList = Dir($_install + $RouteDir + "\*.gpx", "F")
$strPathFileList = $strPathFileList + Dir($_install +  $RouteDir + "\*.kml", "F")


If ShowForm($frmMain) 
        If $btnOK              
                
        Else
                Cancel  Msg="Macro Cancelled"
        EndIf
EndIf


$strFilePath = $_Install + $RouteDir
$strFile= $strFilePath + "\" + $cboPathFile

$strFileExtension = Right($strFile, 3)

if Lower($strFileExtension) = "gpx"
	GoSub name=ProcessGPXFile
Else
	GoSub name=ProcessKMLFile
EndIf



$DynamicFilter = Replace("DynamicArcFilter", "$strFilter", $Filter, True) 
$DynamicFilter = Replace("DynamicDistance", "$txtDistance", $DynamicFilter, True) 

ShowStatus msg="Applying filter, please wait..." Width=350
MacroSet Dialog=Filter VarName=$DynamicFilter
Filter Name=<macro>
ShowStatus Msg="Done" Display=off

BeginSub name=ProcessGPXFile
	ShowStatus msg="Parsing the GPX File, please wait..." Width=350
	# Get the file
	$strKML = GetFile($strFile)
	
	#Strip out the extra crap
	$strKML = Extract($strKML, "<rte>", 2)
	$strKML = Extract($strKML, "</rtecoordinates>", 1)
	
	$strKML = RegExReplace("/><rtept", $strKML, "/>$_NewLine<rtept" )
	
	$intLoop = RegExCount("rtept", $strKML ) + 1
	
	$regex="([-]?\d*\.\d*)"
	$intLoop = 1
	$strFilter = ""
	
	While $intLoop <= RegExCount("rtept", $strKML) + 1
		ShowStatus Msg="Processing Line #" $intLoop
		$strLine=RegExData("(<rtept.*/>)", $strKml, $intLoop)
	        $strLat = RegExSub($regex, $strLine, 1, 1)
	        $strLon = RegExSub($regex, $strLine, 2, 1)
	        If $strLat <> "" and $strLon <> ""
	                $strFilter = $strFilter + "~" + $strLat + "," + $strLon
	        EndIf
	        $intLoop = $intLoop + 1
	EndWhile
EndSub

BeginSub name=ProcessKMLFile
	ShowStatus msg="Parsing the KML File, please wait..." Width=350
	# Get the file
	$strKML = GetFile($strFile)
	#Strip out the extra crap
	$strKML = Extract($strKML, "<LineString>", 2)
	$strKML = Extract($strKML, "<coordinates>", 2)
	$strKML = Extract($strKML, "</coordinates>", 1)

	$intLoop = 1
	$strFilter = ""

	While $intLoop <= RegExCount(" ", $strKML) + 1
        	$strLat = AllTrim(Extract(Extract($strKML, " ", $intLoop) , ",", 2))
	        $strLon = AllTrim(Extract(Extract($strKML, " ", $intLoop) , ",", 1))
	        If $strLat <> "" and $strLon <> ""
	                $strFilter = $strFilter + "~" + $strLat + "," + $strLon
	        EndIf
        	$intLoop = $intLoop + 1
	EndWhile
EndSub

<Data> VarName=$frmMain
#********************************************************************
# Form generated by GSAK form designer on Wed 24-Jun-2009 09:13:16
#********************************************************************

Name = frmMain
  Type = Form
  Caption = Caches Along A Route Options
  Height = 244
  Width = 489

Name = Lbl18
  Type = Label
  Height = 13
  Left = 8
  Top = 16
  Width = 222
  Caption = Find Waypoints that are this far from your route:

Name = btnOK
  Type = Button
  Enter = yes
  Height = 25
  Left = 388
  Top = 12
  Width = 75
  Taborder = 8
  Caption = OK

Name = btnCancel
  Type = Button
  Escape = no
  Height = 25
  Left = 390
  Top = 58
  Width = 75
  Taborder = 9
  Caption = Cancel

Name = txtDistance
  Type = Edit
  Height = 21
  Left = 233
  Top = 13
  Width = 121
  Taborder = 10

Name = cboPathFile
  Type = Combobox
  Height = 21
  Left = 110
  Top = 38
  Values = $strPathFileList
  Width = 247
  Taborder = 11

Name = Label1
  Type = Label
  Height = 13
  Left = 8
  Top = 40
  Width = 57
  Caption = Path File

<enddata>

<data> VarName=$Filter
edtArcDistance=DynamicDistance
chkArcExclude=False
chkNotFound=True
chkFound=False
chkAvailable=True
chkArchivedOnly=False
chkTempUnavailable=False
chkExclude=True
rbtArc=True
rbtPoly=False
rbtPoint=False
rbtReplace=True
rbtAppend=False
ArcFilter=DynamicArcFilter
<enddata>
